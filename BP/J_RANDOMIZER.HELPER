SUBROUTINE J_RANDOMIZER.HELPER(PROCESS.NAME)
** INFORMATION ****************************************************************
*   Routine Name : J_RANDOMIZER.HELPER
*           Type : SUBROUTINE
*         Params : PROCESS.NAME
*            Loc : BP
** AUDIT **********************************************************************
*   Info Updated : 20210302 at 21.53.22 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE BP I_UTIL.H
$INCLUDE BP F_UTIL.LOG.EVENT.H
$INCLUDE BP F_SIENA.TXN.TEMPLATES.H
$INCLUDE BP F_SIENA.TXN.OUT.H
$INCLUDE BP F_SIENA.CACHE.H
** INITIALISE
   PROCESS.NAME = SYSTEM(45)
   CALL U_START(PROCESS.NAME)
* Setup some temp locations for input data

   ERROR.TEXT = ""; STOP.ON.ERROR = @TRUE ; VERBOSE = @TRUE
** Open Files
   FN.SIENA.TXN.TEMPLATES = S_TXN_TEMPLATE_FILE.NAME ; FV.SIENA.TXN.TEMPLATES = ""
   CALL U_IO.OPENFILE(FN.SIENA.TXN.TEMPLATES, FV.SIENA.TXN.TEMPLATES, STOP.ON.ERROR, ERROR.TEXT)
   FN.SIENA.TXN.OUT = S_TXN_OUT_FILE.NAME ; FV.SIENA.TXN.OUT = ""
   CALL U_IO.OPENFILE(FN.SIENA.TXN.OUT, FV.SIENA.TXN.OUT, STOP.ON.ERROR, ERROR.TEXT)
   FN.SIENA.CACHE = S_CACHE_FILE.NAME ; FV.SIENA.CACHE = ""
   CALL U_IO.OPENFILE(FN.SIENA.CACHE, FV.SIENA.CACHE, STOP.ON.ERROR, ERROR.TEXT)

** Setup static
   cacheSampleSize = 10 ; cacheSampleFreq = 10 ; generateNoItems = 10
   amountRangeStart = 100
   amountRangeEnd = 1000
   CALL U_IO.GET.PROPERTY("randomizer", "cacheSampleSize", cacheSampleSize)
   CALL U_IO.GET.PROPERTY("randomizer", "cacheSampleFreq", cacheSampleFreq)
   CALL U_IO.GET.PROPERTY("randomizer", "generateNoItems", generateNoItems)
   CALL U_IO.GET.PROPERTY("randomizer", "amountRangeStart", amountRangeStart)
   CALL U_IO.GET.PROPERTY("randomizer", "amountRangeEnd", amountRangeEnd)




** Get Soft Translation Variables
   SOFT.STMT = "SELECT ":S_CACHE_FILE.NAME:" SAVING UNIQUE ":S_CACHE_OBJECT

   CALL U_GET.LIST(SOFT.STMT, SOFT.LIST, NO.SOFT.ITEMS)
   CALL U_CRT.INFO("NO SOFTS", NO.SOFT.ITEMS)
   CALL U_CRT.RECORD(SOFT.LIST)
   FOR THIS.SUB = 1 TO NO.SOFT.ITEMS
      SUBS.VAR = WC$PREFIX:SOFT.LIST<THIS.SUB>:WC$SUFFIX
      CALL U_CRT.INFO("VAR", SUBS.VAR)
   NEXT THIS.SUB

   TEMPLATE.STMT = "SELECT ":S_TXN_TEMPLATE_FILE.NAME
   CALL U_CRT.INFO("generateNoItems", generateNoItems)
   CALL U_GET.LIST(TEMPLATE.STMT, TEMPLATE.LIST, NO.TEMPLATE.ITEMS)
   CALL U_CRT.INFO("NO TEMPLATE", NO.TEMPLATE.ITEMS)
   CALL U_CRT.RECORD(TEMPLATE.LIST)

   UNIQUE.ID = ""
   CALL U_GET.UUID(UNIQUE.ID)
   SEQ.NO = 1
   FOR THIS.ITEM = 1 TO generateNoItems
      FOR THIS.TEMPLATE = 1 TO NO.TEMPLATE.ITEMS
         IF TEMPLATE.LIST<THIS.TEMPLATE> <> ".keep" THEN
            CALL U_CRT.INFO("TEMPLATE", THIS.TEMPLATE)
            CALL U_CRT.INFO("PROCESS", TEMPLATE.LIST<THIS.TEMPLATE>)

            R.TEMPLATE = ""
            ID.SIENA.TXN.TEMPLATES=TEMPLATE.LIST<THIS.TEMPLATE>
            CALL U_IO.READ(FV.SIENA.TXN.TEMPLATES, ID.SIENA.TXN.TEMPLATES, R.TEMPLATE, STOP.ON.ERROR, ERROR.TEXT)
            CALL U_CRT.RECORD(R.TEMPLATE)

            R.WORKING = R.TEMPLATE

            REPLACE.ITEM = "MSG.ID"
            REPLACE.VALUE = UNIQUE.ID

            GOSUB REPLACE.IT

            REPLACE.ITEM = "TODAY"
            REPLACE.VALUE = CHANGE(OCONV(DATE(), "DYMD[4,2,2]"), " ", "-")
            GOSUB REPLACE.IT

            REPLACE.ITEM = "VALUE.DATE"
            VDATE = DATE()+2 ; NEXT.WORKING.DAY=""
            CALL U_GET.NEXT.WORKING.DAY(VDATE, NEXT.WORKING.DAY)
            REPLACE.VALUE = CHANGE(OCONV(NEXT.WORKING.DAY, "DYMD[4,2,2]"), " ", "-")
            GOSUB REPLACE.IT

            REPLACE.ITEM = "NOW"
            REPLACE.VALUE = OCONV(TIME(), "MTS")
            GOSUB REPLACE.IT

            REPLACE.ITEM = "EXT.REF"
            REPLACE.VALUE = ID.SIENA.TXN.TEMPLATES[1, LEN(ID.SIENA.TXN.TEMPLATES)-4]:CHANGE(THIS.TEMPLATE"R#04", " ", "0")

            GOSUB REPLACE.IT

            REPLACE.ITEM = "AMOUNT"
            REPLACE.VALUE = ""
            CALL U_RND.BETWEEN(REPLACE.VALUE, amountRangeStart, amountRangeEnd)

            GOSUB REPLACE.IT

            REPLACE.ITEM = "SEQ"
            REPLACE.VALUE = CHANGE(SEQ.NO "R#06", " ", "0")
            GOSUB REPLACE.IT

            CPARTY.ID = ""
            FOR REPLACE.SOFT = 1 TO NO.SOFT.ITEMS
               REPLACE.ITEM = SOFT.LIST<REPLACE.SOFT>
               CALL DB_CACHE.PULL(REPLACE.ITEM, REPLACE.VALUE)
               GOSUB REPLACE.IT
               IF REPLACE.ITEM = "COUNTERPARTY" THEN
                  CPARTY.ID = REPLACE.VALUE
               END
            NEXT REPLACE.SOFT

            REPLACE.ITEM = "RegTRDID"
            REPLACE.VALUE = CHANGE(CPARTY.ID[1, 5]:THIS.TEMPLATE"R#05", " ", "0")
            GOSUB REPLACE.IT

            CALL U_CRT.RECORD(R.WORKING)

            ID.SIENA.TXN.OUT = ""
            CALL U_GET.UUID(ID.SIENA.TXN.OUT)
            ID.SIENA.TXN.OUT := ".xml"
            CALL U_IO.WRITE(FV.SIENA.TXN.OUT, ID.SIENA.TXN.OUT, R.WORKING, STOP.ON.ERROR, ERROR.TEXT)


         END
         SEQ.NO += 1
      NEXT THIS.TEMPLATE

   NEXT THIS.ITEM




   CALL U_STOP(PROCESS.NAME)

   RETURN

REPLACE.IT:
   WRK.REPLACE.ITEM = WC$PREFIX:REPLACE.ITEM:WC$SUFFIX
   R.WORKING = CHANGE(R.WORKING, WRK.REPLACE.ITEM, REPLACE.VALUE)
   RETURN

END
