SUBROUTINE U_CRT(TEXT, ITEM.NAME, CALLING.PROCESS)
** INFORMATION ****************************************************************
*   Routine Name : U_CRT
*           Type : SUBROUTINE
*         Params : TEXT, ITEM.NAME, CALLING.PROCESS
*            Loc : BP
** AUDIT **********************************************************************
*   Info Updated : 20210302 at 21.53.22 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE BP I_UTIL.H


   IN.TEXT = TEXT
   TEXT = CONVERT(@AM, T$AM, TEXT)
   TEXT = CONVERT(@VM, T$VM, TEXT)
   TEXT = CONVERT(@SM, T$SM, TEXT)

   dateTimeFormat = "" ; outputType = ""; timeZone = "" ; csvChar = ","

   CALL U_IO.GET.PROPERTY("util", "outputMode", outputMode)
   CALL U_IO.GET.PROPERTY("util", "outputType", outputType)
   CALL U_IO.GET.PROPERTY("util", "csvChar", csvChar)
   CALL U_IO.GET.PROPERTY("util", "timeZone", timeZone)

   OUTPUT.DATE = ""
   CALL U_GET.EPOCH.DATE(OUTPUT.DATE)
   OUTPUT.TEXT = ""

   BEGIN CASE
      CASE outputMode = L$MODE.log4j


         * 20191203 UTC 16:15:39.089 INFO  NetClientImpl Client connecting on [localhost/127.0.0.1:7397]..
         * 20191203 UTC 16:15:39.121 INFO  NetChannelHandler Channel open

         OUTPUT.TEXT := OUTPUT.DATE
         OUTPUT.TEXT := " ":outputType
         OUTPUT.TEXT := " ":CALLING.PROCESS
         OUTPUT.TEXT := " ":ITEM.NAME
         OUTPUT.TEXT := " ":TEXT

      CASE outputMode = L$MODE.csv
         * "20191203 UTC 16:15:39.089","INFO","NetClientImpl Client connecting on [localhost/127.0.0.1:7397]"
         OUTPUT.TEXT := DQUOTE(OUTPUT.DATE)
         OUTPUT.TEXT := csvChar:DQUOTE(outputType)
         OUTPUT.TEXT := csvChar:DQUOTE(CALLING.PROCESS)
         OUTPUT.TEXT := csvChar:DQUOTE(ITEM.NAME)
         OUTPUT.TEXT := csvChar:DQUOTE(TEXT)

      CASE outputMode = L$MODE.simple
         OUTPUT.TEXT := "[":@(T$BOLD_ON):CONVERT(":", "", OCONV(TIME(), "MTS")):@(T$BOLD_OFF)
         OUTPUT.TEXT := "|":@(T$BOLD_ON):CALLING.PROCESS"L#15":@(T$BOLD_OFF)
         OUTPUT.TEXT := "|":@(T$BOLD_ON):UPCASE(ITEM.NAME)"L#10":@(T$BOLD_OFF)
         OUTPUT.TEXT := "][":TEXT:"]"

      CASE 1
         OUTPUT.TEXT := "[":@(T$BOLD_ON):OUTPUT.DATE:@(T$BOLD_OFF)
         OUTPUT.TEXT := "|":@(T$BOLD_ON):outputType:@(T$BOLD_OFF)
         OUTPUT.TEXT := "|":@(T$BOLD_ON):CALLING.PROCESS"L#20":@(T$BOLD_OFF)
         OUTPUT.TEXT := "|":@(T$BOLD_ON):UPCASE(ITEM.NAME)"L#15":@(T$BOLD_OFF)
         OUTPUT.TEXT := "][":TEXT:"]"
   END CASE
   DISPLAY @(T$CLEARLINE):OUTPUT.TEXT

   TEXT = IN.TEXT

   RETURN
END
