PROGRAM GIT
** INFORMATION ****************************************************************
*   Routine Name : GIT
*           Type : SUBROUTINE
*         Params :
*            Loc : BP
** AUDIT **********************************************************************
*   Info Updated : 20210228 at 16.21.20 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE BP I_UTIL.H
   CALL U_INITIALISE("GIT")
   PNM="GIT"
   CALL U_START(PNM)
   CALL U_HEADER("GIT")
   CMD.STRING = CONVERT(SPACE(1), @AM, @SENTENCE)
   IF CMD.STRING<2> = "" THEN
      CALL U_CRT.INFO("ERROR", "Parameters [git parameters]")
      CALL U_STOP(PNM)
      STOP
   END

   DEL CMD.STRING<1>
   CMD.STRING = CONVERT(@AM, SPACE(1), CMD.STRING)
   IF UPCASE(CMD.STRING) = "INSTALL" THEN

      GOSUB AUTO.COMPILE

   END ELSE
      STMT.1 = "git ":CMD.STRING
      CALL U_OS.COMMAND(STMT.1, @TRUE, "", @FALSE)
   END

   STOP


AUTO.COMPILE:
   BASIC.LIST = "" ; CATALOG.LIST = "" ; FORMAT.LIST = ""
   CALL U_IO.GET.LIST("autoCompile", "COMPILE", BASIC.LIST, NO.BASIC.ITEMS)
   CALL U_IO.GET.LIST("autoCompile", "CATALOG", CATALOG.LIST, NO.CATALOG.ITEMS)


   STMTS = ""
   STMTS<-1> = "git pull"

   FOR I = 1 TO NO.BASIC.ITEMS
      STMTS<-1> = "BASIC ":BASIC.LIST<I>:" *"
   NEXT I
   FOR J = 1 TO NO.CATALOG.ITEMS
      STMTS<-1> = "CATALOG ":CATALOG.LIST<J>:" *"
   NEXT J

   NO.ACTIONS = DCOUNT(STMTS, @AM)
   FOR K = 1 TO NO.ACTIONS
      CALL U_OS.EXECUTE(STMTS<K>, @TRUE)
   NEXT K
   RETURN
END
