SUBROUTINE U_BUILD.SERVICE.CATALOG.HELPER(SPARE01)
** INFORMATION ****************************************************************
*   Routine Name : U_BUILD.SERVICE.CATALOG.HELPER
*           Type : SUBROUTINE
*         Params : SPARE01
*            Loc : BP
** AUDIT **********************************************************************
*   Info Updated : 20210411 at 16.25.52 in MWT-QM-DEV by matttownsend
*                : on silicon.local (Mac)
*******************************************************************************

$INCLUDE BP I_UTIL.H
$INCLUDE BP F_UTIL.WCT.RECV.H
$INCLUDE BP F_UTIL.WCT.RESP.H
$INCLUDE BP F_UTIL.WCT.TEMPLATES.H
$INCLUDE BP F_UTIL.WORKFILE.H
$INCLUDE BP F_WEB.SESSIONS.H
$INCLUDE BP F_UTIL.CONFIG.H

   PROCESS.NAME = "U_BUILD.SERVICE.CATALOG"
   wct_Properties = "wct_connect"
   FN.VOC = "VOC" ; FV.VOC = "" ; STOP.ON.ERROR = @TRUE ; ERROR.TEXT = ""
   CALL U_IO.OPENFILE(FN.VOC, FV.VOC, STOP.ON.ERROR, ERROR.TEXT)

   serviceCatalogID = "" ; CALL U_IO.GET.PROPERTY(wct_Properties, "serviceCatalogID", serviceCatalogID)
   serviceCatalogItemsID = "" ; CALL U_IO.GET.PROPERTY(wct_Properties, "serviceCatalogItemsID", serviceCatalogItemsID)
   serviceID = "" ; CALL U_IO.GET.PROPERTY(wct_Properties, "serviceID", serviceID)
   menuID = "" ; CALL U_IO.GET.PROPERTY("util", "vocMenu", menuID)

   autoGenCatalogItems = ""
   GOSUB BUILD.DATAMAP
   GOSUB BUILD.REPORTS
   GOSUB BUILD.JOBS

   manualCatalogItems = ""
   CALL U_IO.READ(FV.UTIL.CONFIG, serviceCatalogItemsID:".list", manualCatalogItems, @FALSE, "")
   newCatalog = autoGenCatalogItems:@AM:"# FOLLOWING THIS LINE IS CONTENT FROM ":DQUOTE(serviceCatalogID):@AM:manualCatalogItems
   CALL U_IO.WRITE(FV.UTIL.CONFIG, serviceCatalogID:".list", newCatalog, @FALSE, "")

   servicesList = ""
   servicesCount = 0

   CALL U_IO.GET.LIST(serviceCatalogID, serviceID, servicesList, servicesCount)

   servicesList = CHANGE(servicesList, U$SEP.CFG.DATA, @VM)

   R.MENU = ""
   R.MENU<1> = "M"
   MENU.HEADING = "Service Catalog"
   CALL U_TRANSLATE("UTIL", MENU.HEADING, 1, MENU.HEADING)
   R.MENU<2> = MENU.HEADING

   menuOptionName = 3 ; servicesName = 1
   menuCommand = 4 ; servicesCommand = 2 ; servicesItem = 3 ; servicesOptions = 4
   menuHelptext = 5 ; serviceHelptext = 5

   FOR I = 1 TO servicesCount
      IF servicesList<I, servicesCommand>[1] # "@" THEN
         R.MENU<menuOptionName, I> = servicesList<I, servicesName>
         thisCommand = ""
         thisCommand = CHANGE(servicesList<I, servicesCommand>, "!", "")
         IF servicesList<I, servicesItem> <> "" THEN
            thisCommand := " ":servicesList<I, servicesItem>
         END
         IF servicesList<I, servicesOptions> <> "" THEN
            thisCommand := " ":servicesList<I, servicesOptions>
         END
         IF thisCommand <> "" THEN
            thisCommand := ";"
         END
         R.MENU<menuCommand, I> = thisCommand
         R.MENU<menuHelptext, I> = servicesList<I, serviceHelptext>
      END
   NEXT I

   * CALL U_CRT.INFO("menu", R.MENU)

   CALL U_IO.WRITE(FV.VOC, menuID, R.MENU, STOP.ON.ERROR, ERROR.TEXT)

   *CALL U_CRT.INFO("NOSERV", servicesCount)
   CALL U_CRT.INFO("REBUILD", "Menu rebuilt from ":DQUOTE(serviceCatalogItemsID):" as ":DQUOTE(menuID))

   CALL U_STOP(PROCESS.NAME)
   RETURN

BUILD.DATAMAP:
   TITLE = "Transaction Generation"
   CALL U_MSG(TITLE, "")
   dataMapExtn = "" ; CALL U_IO.GET.PROPERTY("util", "dataMapExtn", dataMapExtn)
   dataMapPrefix = "" ; CALL U_IO.GET.PROPERTY("util", "dataMapPrefix", dataMapPrefix)

   STMT = "SELECT ":U$CONFIG.FILE.NAME:" LIKE ":dataMapPrefix:"...":dataMapExtn
   * CALL U_CRT.INFO("STMT", STMT)
   txnMenu.Items = "" ; txnMenu.Count = 0
   CALL U_GET.LIST(STMT, txnMenu.Items, txnMenu.Count)


   txnItems = ""
   autoGenCatalogItems<-1> = serviceID:U$SEP.CFG:TITLE:U$SEP.CFG.DATA:"":U$SEP.CFG.DATA:"":U$SEP.CFG.DATA:"":U$SEP.CFG.DATA:U$SEP.CFG
   FOR I = 1 TO txnMenu.Count
      txn.ID = CHANGE(CHANGE(txnMenu.Items<I>, dataMapExtn, ""), dataMapPrefix, "")
      autoGenCatalogItems<-1> = serviceID:U$SEP.CFG:txn.ID:U$SEP.CFG.DATA:"!U_GEN.TXNS":U$SEP.CFG.DATA:UPCASE(txn.ID):U$SEP.CFG.DATA:"NOPAGE":U$SEP.CFG.DATA:U$SEP.CFG
   NEXT I


   RETURN

BUILD.REPORTS:
   TITLE = "Reports and Queries"
   CALL U_MSG(TITLE, "")
   reports.REC = "" ; reports.Count = 0
   autoGenCatalogItems<-1> = serviceID:U$SEP.CFG:TITLE:U$SEP.CFG.DATA:"":U$SEP.CFG.DATA:"":U$SEP.CFG.DATA:"":U$SEP.CFG.DATA:U$SEP.CFG
   CALL U_IO.READ(FV.UTIL.CONFIG, "reports.properties", reports.REC, @FALSE, "")
   reports.Count = DCOUNT(reports.REC, @AM)
   FOR thisReport = 1 TO reports.Count
      thisReportItem = reports.REC<thisReport>
      IF thisReportItem[1, 1] = "#" THEN
         * IGNORE ITS A COMMENT
      END ELSE
         thisReportItem = CHANGE(thisReportItem, U$SEP.CFG, @AM)
         thisReportItem = CHANGE(thisReportItem, U$SEP.CFG.DATA, @AM)
         itemTitle = thisReportItem<1> ; IF thisReportItem<5> # "" THEN itemTitle = thisReportItem<5>
         autoGenCatalogItems<-1> = serviceID:U$SEP.CFG:itemTitle:U$SEP.CFG.DATA:"REPORT.DO":U$SEP.CFG.DATA:thisReportItem<1> :U$SEP.CFG.DATA:"NOPAGE":U$SEP.CFG.DATA:U$SEP.CFG
      END
   NEXT thisReport
   RETURN

BUILD.JOBS:
   TITLE = "Application Maintenance"
   CALL U_MSG(TITLE, "")
   jobs.REC = "" ; jobs.Count = 0
   autoGenCatalogItems<-1> = serviceID:U$SEP.CFG:TITLE:U$SEP.CFG.DATA:"":U$SEP.CFG.DATA:"":U$SEP.CFG.DATA:"":U$SEP.CFG.DATA:U$SEP.CFG

   CALL U_IO.READ(FV.UTIL.CONFIG, "jobs.properties", jobs.REC, @FALSE, "")

   jobs.Count = DCOUNT(jobs.REC, @AM)
   *   CALL U_CRT.INFO("reports.Count", reports.Count)
   FOR thisJob = 1 TO jobs.Count
      thisJobItem = jobs.REC<thisJob>
      IF thisJobItem[1, 1] = "#" THEN
         * IGNORE ITS A COMMENT
      END ELSE
         thisJobItem = CHANGE(thisJobItem, U$SEP.CFG, @AM)
         *CALL U_CRT.RECORD(thisJobItem)
         thisJobDesc = ""
         thisJobDesc = thisJobItem<3>
         thisJobItem = CHANGE(thisJobItem, U$SEP.CFG.DATA, @AM)
         *CALL U_CRT.RECORD(thisJobItem)
         itemTitle = thisJobItem<1> ; IF thisJobDesc # "" THEN itemTitle = thisJobDesc
         autoGenCatalogItems<-1> = serviceID:U$SEP.CFG:itemTitle:U$SEP.CFG.DATA:"JOB.DO":U$SEP.CFG.DATA:thisJobItem<1> :U$SEP.CFG.DATA:"NOPAGE":U$SEP.CFG.DATA:U$SEP.CFG
      END
   NEXT thisJob
   * CALL U_CRT.RECORD(autoGenCatalogItems)
   RETURN


END
