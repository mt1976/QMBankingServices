      SUBROUTINE GOOGLEMAPS(ADDRESS,CITY,STATE,ZIP,MAPPREF)
      *
      * from PCMJOE with inspiration and examples from LIVEBLUES and WDAVIDHICKS
      *
      * SAMPLE PROGRAM FOR ACCUTERM FORUM
      *
      EQU STX TO CHAR(2), CR TO CHAR(13), EM TO CHAR(25), ESC TO CHAR(27), VM TO CHAR(253)
      **
      **** CHOOSE WHICH WEB MAP SERVICE YOU WANT
      **
      *      LOOP WHILE 1 DO
      *         CRT; CRT 'ENTER "M" FOR MAPQUEST, "Y" FOR YAHOO, "G" FOR GOOGLE or Enter to quit':
      *         INPUT MAPPREF,1
         IF MAPPREF='' THEN STOP
         GOSUB 200; * get your address and format it
         GOSUB 100; * show the map!
      *      REPEAT
      *
100   * get the map
      BEGIN CASE
         CASE MAPPREF='G'; * GOOGLE
            URL='http://maps.google.com/maps?q='
            URLADDR = ADDRESS:',+':CITY:',+':STATE:'+':ZIP
            URL=URL:URLADDR:'&hl=en'
         CASE MAPPREF='Y'; * YAHOO
            URLADDR = ADDRESS:'&csz=':CITY:'%2C+':STATE:'+':ZIP
            URL = 'http://maps.yahoo.com/maps_result?addr=':URLADDR
         CASE MAPPREF='M'; * MAPQUEST
            URLADDR=ADDRESS:'&city=':CITY:'&state=':STATE:'&zipcode=':ZIP
            URL='http://www.mapquest.com/maps/map.adp?&address=':URLADDR
         CASE 1; RETURN
      END CASE
      CRT ESC:STX:'<':URL:CR:
      RETURN
      *
200   * Get address string ready
      **
      **** pass STREET ADDRESS, CITY, STATE AND ZIP from your database
      **
      * here's my office location
      *      ADDRESS='1701 S. WALTON BLVD.'
      *      CITY='BENTONVILLE'
      *      STATE='AR'
      *      ZIP='72712'
      STRING=ADDRESS
      GOSUB 250
      ADDRESS=URL.STRING
      STRING=CITY
      GOSUB 250
      CITY=URL.STRING
      IF MAPPREF='G' THEN
         IF ZIP MATCH "9N" THEN ZIP=OCONV(ZIP,'MR#####-####')
      END
      RETURN
      *
250   * GET READY FOR URL, REPLACE SYMBOLS WITH THEIR HEX EQUIVALENTS
      URL.STRING = ''
      NUMC=LEN(STRING)
      FOR X = 1 TO NUMC
         CH=STRING[X,1]
         HCH = OCONV(CH,'MX0C')
         BEGIN CASE
            CASE HCH = '20'; * space
               URL.STRING = URL.STRING:'+'
            CASE HCH = '2D'; * hyphen
               URL.STRING = URL.STRING:'-'
            CASE HCH = '2E'; * period
               IF MAPPREF='G' THEN
                  URL.STRING=URL.STRING:'%2E'
               END ELSE URL.STRING=URL.STRING:CH; * mapquest and yahoo use '.'
            CASE HCH GE '21' AND HCH LE '2F'; * symbol char 33 to char 47
               URL.STRING = URL.STRING:'%':HCH
            CASE HCH GE '3A' AND HCH LE '40'; * symbol char 58 to char 64
               URL.STRING = URL.STRING:'%':HCH
            CASE HCH GE '5B' AND HCH LE '60'; * symbol char 91 to char 96
               URL.STRING = URL.STRING:'%':HCH
            CASE HCH GE '7B' AND HCH LE '7E'; * symbol char 123 to char 126
               URL.STRING = URL.STRING:'%':HCH
            CASE 1
               URL.STRING = URL.STRING:CH
         END CASE
      NEXT X
      RETURN