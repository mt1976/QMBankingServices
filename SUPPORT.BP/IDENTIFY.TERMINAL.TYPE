*
**************************************************************************
*Copyright (c) 1993-2000 Schellenbach & Associates,Inc. as an unpublished*
*work All rights reserved. This work is the property of and embodies     *
*trade secrets and confidential information proprietary to Schellenbach &*
*Associates, Inc.  It may not be reproduced, copied, used, disclosed,    *
*transferred, adapted or modified without the express written approval   *
*of Schellenbach & Associates Inc., except as provided for in the        *
*accompanying warranty notice and licensing agreement.                   *
**************************************************************************
*
* THIS PROGRAM IDENTIFIES THE TERMINAL BY SENDING VARIOUS
* COMMANDS AND CHECKING THE RESPONSE FROM THE TERMINAL.
*
**************************************************************************
*
EQU WY50.TYPE TO 'W', WY60.TYPE TO 'L', ADDS.TYPE TO 'V', ANSI.TYPE TO 'J'
*
EQU ESC TO CHAR(27), STX TO CHAR(2), VT TO CHAR(11), CR TO CHAR(13), CAN TO CHAR(24)
*
LOOP WHILE SYSTEM(14) DO INPUT X,0 REPEAT ;* CLEAR TYPE-AHEAD BUFFER
*
PRINT ESC:'[0c':     ;* CHECK FOR ANSI TERMINAL
GOSUB 100            ;* GET RESPONSE
IF RESPONSE NE '' THEN TYPE=ANSI.TYPE; GOTO 99
PRINT ESC:' ':       ;* TRY THE WYSE 'IDENTIFY TERMINAL' COMMAND
GOSUB 100            ;* GET RESPONSE (IF ANY)
IF RESPONSE NE '' THEN
 PRINT ESC:'*':      ;* CLEAR SCREEN & HOME CURSOR
 PRINT ESC:'G0':     ;* PRINT AN ATTRIBUTE TO SEE IF CURSOR ADVANCES
 PRINT ESC:'?':      ;* READ CURSOR ADDRESS
 GOSUB 100           ;* GET RESPONSE
 IF RESPONSE EQ '20200D' THEN TYPE=WY60.TYPE; GOTO 99
 IF RESPONSE EQ '20210D' THEN
  PRINT ESC:'=! ': ;* POSITION TO 0,1
  PRINT VT:STX:      ;* POSITION TO LINE 2 IF VP-ENH
  PRINT ESC:'?':     ;* READ CURSOR ADDRESS
  GOSUB 100          ;* GET RESPONSE
  IF RESPONSE EQ '22200D' THEN TYPE=ADDS.TYPE; GOTO 99
  IF RESPONSE EQ '20200D' THEN TYPE=WY50.TYPE; GOTO 99
 END
END
* NOT FOUND, PROMPT FOR TERM TYPE...
FOR I=1 TO 24; PRINT; NEXT I
LOOP
PRINT 'Terminal not identified. Please enter your terminal type ':
INPUT TYPE,1
UNTIL TYPE MATCHES '1A' DO NULL REPEAT
TYPE=OCONV(TYPE,'MCU')
PRINT
*
99 EXECUTE 'TERM ':TYPE
PRINT @(-1):
STOP
*
100 * GET TERMINAL RESPONSE (IN HEX)
ECHO OFF
RESPONSE=''
T=SYSTEM(12)+250
LOOP UNTIL SYSTEM(12)>T DO
 IF SYSTEM(14) THEN
  LOOP WHILE SYSTEM(14) DO
   INPUT X,0
   X=OCONV(X,'MX')
   RESPONSE=RESPONSE:X
  REPEAT
  T=SYSTEM(12)+50
 END
REPEAT
ECHO ON
PRINT ESC:ESC:CR:CR:
RETURN
END
