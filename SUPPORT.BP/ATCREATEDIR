SUBROUTINE ATCREATEDIR(PATH,STAT)
* Test for existing directory or create new one using AccuTerm Script
* Returns 0 if error, 1 if directory already exists, or 2 if new directory
* was created.
EQU ESC TO CHAR(27), STX TO CHAR(2), CR TO CHAR(13), EM TO CHAR(25)
SCR='On Error Resume Next'
SCR=SCR:EM:'Dim n As Long, m As Long'
SCR=SCR:EM:'Dim Path As String'
SCR=SCR:EM:'Path = "':PATH:'"'
SCR=SCR:EM:'n = 0'
SCR=SCR:EM:'n = (GetAttr(Path) And 16) / 16'
SCR=SCR:EM:'If n = 0 Then'
SCR=SCR:EM:'n = 1'
SCR=SCR:EM:'On Error Resume Next'
SCR=SCR:EM:'While n < Len(Path)'
SCR=SCR:EM:'m = InStr(n, Path, "\")'
SCR=SCR:EM:'If m = 0 Then m = Len(Path) + 1'
SCR=SCR:EM:'MkDir Left$(Path, m - 1)'
SCR=SCR:EM:'n = m + 1'
SCR=SCR:EM:'Wend'
SCR=SCR:EM:'n = 0'
SCR=SCR:EM:'n = (GetAttr(Path) And 16) / 8'
SCR=SCR:EM:'End If'
SCR=SCR:EM:'InitSession.Output CStr(n) & Chr$(13)'
PRINT ESC:STX:'P':SCR:CR:
ECHO OFF
PROMPT ''
INPUT STAT:
PROMPT '?'
ECHO ON
RETURN
END