SUBROUTINE ATSENDFAX(FAXNBR, FAXNAME, FAXCOMPANY, FAXSUBJECT, FAXCOVERPAGE, FAXCOVERTEXT, FAXFILE, STAT)
*
* ACCUTERM SAMPLE PROGRAM TO SEND A FAX USING WINFAX-PRO
*
* UPDATED 7/10/2003
*
* INPUT INTERFACE:
*  FAXNBR: RECIPIENT FAX NUMBER
*  FAXNAME: RECIPIENT NAME
*  FAXCOMPANY: RECIPIENT COMPANY
*  FAXSUBJECT: SUBJECT LINE
*  FAXCOVERPAGE: FULL PATH TO DESIRED COVER PAGE (.CVP) FILE, OR NULL FOR DEFAULT COVER PAGE
*  FAXCOVERTEXT: TEXT TO BE INCLUDED ON COVER PAGE - SEPARATE MULTIPLE LINES WITH ATTRIBUTE MARKS
*  FAXFILE: FULL PATH OF ANY FILES TO ATTACH - SEPARATER MULTIPLE LINES WITH ATTRIBUTE MARKS
*
* OUTPUT INTERFACE:
*  STAT: NULL IF NO ERRORS, ELSE CONTAINS ERROR MESSAGE RETURNED BY WINFAX
*
* NOTE: YOU WILL PROBABLY GET A "DDE ERROR" IF YOU USE AN INVALID PATH FOR THE COVER PAGE
* FILE NAME, OR ANY ATTACHMENT FILE NAME!
*
* THIS PROGRAM USES ACCUTERM VBA SCRIPT TO CONTROL WINFAX USING DDE
*
EQU ESC TO CHAR(27), STX TO CHAR(2), CR TO CHAR(13), EM TO CHAR(25), AM TO CHAR(254)
TARGET='""':FAXNBR:'"",""':OCONV(TIME(),'MTS'):'"",""':OCONV(DATE(),'D2/')
TARGET=TARGET:'"",""':FAXNAME:'"",""':FAXCOMPANY:'""'
IF FAXSUBJECT NE '' THEN
 TARGET=TARGET:',""':FAXSUBJECT:'""'
END
SCRIPT='On Error Goto HandleError':EM
SCRIPT=SCRIPT:'ch=DDEInitiate("FAXMNG32","CONTROL")':EM
SCRIPT=SCRIPT:'DDEExecute ch,"GoIdle"':EM
SCRIPT=SCRIPT:'DDETerminate ch':EM
SCRIPT=SCRIPT:'ch=DDEInitiate("FAXMNG32","TRANSMIT")':EM
SCRIPT=SCRIPT:'DDEPoke ch,"SendFax","recipient(':TARGET:')"':EM
IF FAXCOVERPAGE NE '' THEN
 SCRIPT=SCRIPT:'DDEPoke ch,"SendFax","setcoverpage(""':FAXCOVERPAGE:'"")"':EM
END
IF FAXCOVERTEXT NE '' THEN
 SCRIPT=SCRIPT:'txt="fillcoverpage("""':EM
 N=DCOUNT(FAXCOVERTEXT,AM)
 FOR I=1 TO N
  ARG=FAXCOVERTEXT<I>; GOSUB 100
  IF I < N THEN
   SCRIPT=SCRIPT:'txt=txt & "':ARG:'" & Chr$(13) & Chr$(10)':EM
  END ELSE
   SCRIPT=SCRIPT:'txt=txt & "':ARG:'"':EM
  END
 NEXT I
 SCRIPT=SCRIPT:'txt=txt & """)"':EM
 SCRIPT=SCRIPT:'DDEPoke ch,"SendFax",txt':EM
END
IF FAXFILE NE '' THEN
 N=DCOUNT(FAXFILE,AM)
 FOR I=1 TO N
  ARG=FAXFILE<I>
  SCRIPT=SCRIPT:'DDEPoke ch,"SendFax","attach(""':ARG:'"")"':EM
 NEXT I
END
SCRIPT=SCRIPT:'DDEPoke ch,"SendFax","ShowSendScreen(""0"")"':EM
SCRIPT=SCRIPT:'DDEPoke ch,"SendFax","SendFaxUI"':EM
SCRIPT=SCRIPT:'DDETerminate ch':EM
SCRIPT=SCRIPT:'ch=DDEInitiate("FAXMNG32","CONTROL")':EM
SCRIPT=SCRIPT:'DDEExecute ch,"GoActive"':EM
SCRIPT=SCRIPT:'DDETerminate ch':EM
SCRIPT=SCRIPT:'InitSession.Output Chr$(13)':EM
SCRIPT=SCRIPT:'Exit Sub':EM
SCRIPT=SCRIPT:'HandleError: ErrMsg = Err.Description':EM
SCRIPT=SCRIPT:'On Error Resume Next':EM
SCRIPT=SCRIPT:'DDETerminate ch':EM
SCRIPT=SCRIPT:'InitSession.Output ErrMsg & Chr$(13)'
PRINT ESC:STX:'P':SCRIPT:CR:
PROMPT ''
ECHO OFF
INPUT STAT:
ECHO ON
PROMPT '?'
RETURN
*
100 * Local subroutine to fixup embedded double-quote marks
K = 1
LOOP
   J = INDEX(ARG, '"', K)
WHILE J DO
   ARG = ARG[1, J] : ARG[J, 99999]
   K = K + 2
REPEAT
RETURN
END
