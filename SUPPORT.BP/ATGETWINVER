SUBROUTINE ATGETWINVER(VER)
* SUBROUTINE TO GET WINDOWS VERSION USING ACCUTERM SCRIPT
*
* Returns platform (WIN9X or WINNT) followed by version number
*
* For example, Windows XP returns 'WINNT 5.1'
*
* Some known version numbers are:
*
*  Windows 95: 3.95 or 4.0
*  Windows 98: 4.10
*  Windows ME: 4.90 (unverified)
*  Windows NT4: 4.0
*  Windows 2000: 5.0
*  Windows XP: 5.1
*  Windows Server 2003: 5.2
*  Windows Server 2003 R2: 5.2
*  Windows Vista: 6.0
*  Windows Server 2008: 6.0
*  Windows 7: 6.1
*  Windows Server 2008 R2: 6.1
*  Windows 8: 6.2
*  Windows Server 2012: 6.2
*
EQU ESC TO CHAR(27), STX TO CHAR(2), CR TO CHAR(13), EM TO CHAR(25)
SCR = 'InitSession.Output GetWinVer() & vbCr'
SCR = SCR : EM : 'End Sub'
SCR = SCR : EM : 'Declare Function GetVersion Lib "Kernel32" () As Long'
SCR = SCR : EM : 'Function GetWinVer() As String'
SCR = SCR : EM : 'Dim lng As Long'
SCR = SCR : EM : 'Dim str As String'
SCR = SCR : EM : 'lng = GetVersion()'
SCR = SCR : EM : 'If lng < 0 Then'
SCR = SCR : EM : 'str = "WIN9X "'
SCR = SCR : EM : 'Else'
SCR = SCR : EM : 'str = "WINNT "'
SCR = SCR : EM : 'End If'
SCR = SCR : EM : 'str = str & CStr(lng And 255) & "." & CStr((lng And 65280) / 256)'
SCR = SCR : EM : 'GetWinVer = str'
SCR = SCR : EM : 'End Function'
SCR = SCR : EM : 'Sub Dummy'
PRINT ESC : STX : 'P' : SCR : CR :
ECHO OFF
PROMPT ''
INPUT VER:
PROMPT '?'
ECHO ON
RETURN
END
