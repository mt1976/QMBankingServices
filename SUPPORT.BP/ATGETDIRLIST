SUBROUTINE ATGETDIRLIST(PATH, DELIM, LIST, MODE)
*
* ACCUTERM SUBROUTINE TO RETRIEVE LIST OF FILES FROM DIRECTORY
* 
*  CALL THIS SUBROUTINE TO RETRIEVE A LIST OF FILES OR
*  FOLDERS FROM A SPECIFIED DIRECTORY. CALL WITH THE MODE
*  PARAMETER SET TO 0 FOR A FILE LIST, SET MODE TO 1 FOR
*  A FOLDER LIST.
*
*  NOTE: THIS SUBROUTINE BUILDS AND EXECUTES A SCRIPT WHICH
*  RETURNS THE FILE LIST BACK TO THE HOST ONE ITEM AT A TIME.
*
EQU ESC TO CHAR(27), STX TO CHAR(2), CR TO CHAR(13), EM TO CHAR(25)
*
IF PATH[LEN(PATH),1] <> '\' THEN PATH = PATH : '\'
IF MODE THEN X = 16 ELSE X = 0
*
SCR = ''
SCR = SCR : 'Dim FilePath As String' : EM
SCR = SCR : 'Dim FileSpec As String' : EM
SCR = SCR : 'Dim FileName As String' : EM
SCR = SCR : 'Dim Attr As Long' : EM
SCR = SCR : 'On Error Resume Next' : EM
SCR = SCR : 'FilePath = "' : PATH : '"' : EM
SCR = SCR : 'FileSpec = FilePath' : EM
SCR = SCR : 'Do' : EM
SCR = SCR : 'FileName = Dir$(FileSpec, 49)' : EM
SCR = SCR : 'FileSpec = ""' : EM
SCR = SCR : 'If Len(FileName) = 0 Then Exit Do' : EM
SCR = SCR : 'Attr = 128' : EM
SCR = SCR : 'Attr = GetAttr(FilePath & FileName)' : EM
SCR = SCR : 'If (Attr And 144) = ' : X : ' And FileName <> "." And FileName <> ".." Then' : EM
SCR = SCR : 'InitSession.Output FileName & Chr$(13)' : EM
SCR = SCR : 'End If' : EM
SCR = SCR : 'Loop' : EM
SCR = SCR : 'InitSession.Output "*EOF*" & Chr$(13)'
*
LIST = ''
N = 0
PROMPT ''
ECHO OFF
PRINT ESC : STX : 'P' : SCR : CR :
LOOP
INPUT ANS:
UNTIL ANS EQ '*EOF*' DO
IF N > 0 THEN LIST = LIST : DELIM : ANS ELSE LIST = ANS
N = N + 1
REPEAT
ECHO ON
PROMPT '?'
*
RETURN
*
END