     OPEN 'KJBP' TO BP ELSE STOP 201,'BP'
     OPEN 'MESSAGES' TO MESSAGES ELSE STOP 201,'MESSAGES'
     HUGE = 999999
     READ AE FROM BP,'AE' ELSE STOP 202,'AE'
     XXNO = DCOUNT(AE,@AM)
     EA = ''
     FOR XX = 1 TO XXNO
        LINE = AE<XX>
        GOSUB FIX.LINE
        EA<-1> = LINE
     NEXT XX
     WRITE EA ON BP,'LED'
     STOP

FIX.LINE:
     POSN = INDEX(LINE,'sysmsg',1)
     IF POSN ELSE RETURN
     LEFT = LINE[1,POSN-1]
     LEFT = CHANGE(LEFT,'display','crt')
     LANE = LINE[POSN+6,HUGE]
     YYNO = LEN(LANE)
     IF LANE[1,1] EQ '(' THEN DONE = 1 ELSE DONE = 0
     PART = ''
     FOR YY = 2 TO YYNO WHILE DONE
        BIT = LANE[YY,1]
        IF BIT EQ '(' THEN DONE += 1
        IF BIT EQ ')' THEN DONE -= 1
        IF DONE THEN PART := BIT
     NEXT YY
     BITE = ''
     IF YY EQ YYNO THEN YY += 1
     IF LANE[YY,1] EQ '<' THEN
        LOOP
           BIT = LANE[YY,1]
           BITE := BIT
           YY += 1
        UNTIL BIT EQ '>' DO
        REPEAT
     END
     BITE = OCONV(BITE,'MCN')

     REST = LANE[YY,HUGE]
     TEST = INDEX(REST,';*',1)
     IF TEST THEN REST = TRIMB(REST[1,TEST-1])

     NUMB = FIELD(PART,',',1)
     FX1 = FIELD(PART,',',2)
     FX2 = FIELD(PART,',',3)
     FX3 = FIELD(PART,',',4)
     FX4 = FIELD(PART,',',5)

     READ MESG FROM MESSAGES, NUMB ELSE RETURN
     IF (NUMB GE '6300' AND NUMB LE '6303') OR (NUMB GE '6317' AND NUMB LT '6350')
        THEN GOSUB PAGE
        ELSE GOSUB NONPAGE
     RETURN

NONPAGE:
     IF BITE THEN PART = MESG<BITE> ELSE PART = MESG<1>

     GOSUB QUOTE

     LINE = LEFT:PART:REST
     RETURN

PAGE:
     LINE = LEFT:"''"
     TEMP = FIELD(TRIM(LEFT),' ',1)
     MARG = SPACE(INDEX(LEFT,TEMP,1)-1)
     LEFT = MARG:TEMP

     ZZNO = DCOUNT(MESG,@AM)
     FOR ZZ = 1 TO ZZNO
        PART = MESG<ZZ>
        GOSUB QUOTE
        LINE<-1> = LEFT:'<-1> = ':PART
     NEXT ZZ
     RETURN


QUOTE:
     THIS = "'"
     IF INDEX(PART,"'",1) THEN
        THIS = '"'
        IF INDEX(PART,'"',1) THEN THIS = '\'
     END
     PART = THIS:PART:THIS
     IF FX1 NE '' THEN PART = CHANGE(PART,'%1',THIS:':':FX1:':':THIS)
     IF FX2 NE '' THEN PART = CHANGE(PART,'%2',THIS:':':FX2:':':THIS)
     IF FX3 NE '' THEN PART = CHANGE(PART,'%3',THIS:':':FX3:':':THIS)
     IF FX4 NE '' THEN PART = CHANGE(PART,'%4',THIS:':':FX4:':':THIS)
     IF PART[1,3] EQ THIS:THIS:':' THEN PART = PART[4,HUGE]
     IF PART[LEN(PART)-2,3] EQ ':':THIS:THIS THEN PART = PART[1,LEN(PART)-3]
     IF PART[LEN(PART)-1,2] EQ THIS:':' THEN PART = PART[1,LEN(PART)-1]
     RETURN
