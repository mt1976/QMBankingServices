SUBROUTINE U_OS.GET.LIST(IN.PROPERTIES.FILE, IN.LIST.NAME, IN.LIST.PREFIX, RETURN.LIST, NO.ITEMS)
** INFORMATION ****************************************************************
* Routine Name   : U_OS.GET.LIST
*         Type   : SUBROUTINE
*         Params : IN.PROPERTIES.FILE, IN.LIST.NAME, IN.LIST.PREFIX, RETURN.LIST, NO.ITEMS
*         Loc    : UTIL.BP
* -----------------------------------------------------------------------------
* Info Updated   : 20191219 at 15.04.21 in MWT-QM-DEV by sales
*                : on ldn-srv-ubnt01 (Linux)
* -----------------------------------------------------------------------------
*
$INCLUDE UTIL.BP I_UTIL.H
$INCLUDE UTIL.BP I_UTIL.LOG.EVENT.H

   RETURN.LIST = ""
   STOP.ON.ERROR = @TRUE
   VERBOSE = @TRUE
   NO.ITEMS = -1

   IF IN.PROPERTIES.FILE <> "" AND IN.LIST.PREFIX <> "" AND IN.LIST.NAME <> "" THEN

      ID.PROPERTIES = IN.LIST.NAME:".list"
      FN.PROPERTIES= IN.PROPERTIES.FILE ; FV.PROPERTIES = ""
      CALL U_OPENFILE(FN.PROPERTIES, FV.PROPERTIES, @TRUE, "")
      CALL U_READ.LOCAL(FV.PROPERTIES, ID.PROPERTIES, R.PROPERTIES, STOP.ON.ERROR, ERROR.TEXT)

      NO.PROPERTIES = DCOUNT(R.PROPERTIES, @AM)
      * CALL U_CRT.INFO("NO.PROPS",NO.PROPERTIES)
      NO.ITEMS = 0
      FOR THIS.LIST = 1 TO NO.PROPERTIES
         TEST.VAL = CONVERT(U_PROP_SEP, @AM, R.PROPERTIES<THIS.LIST>)
         UP.TEST.LIST = UPCASE(TEST.VAL<1>)
         UP.SEARCH.LIST = UPCASE(IN.LIST.PREFIX)

         IF UP.TEST.LIST = UP.SEARCH.LIST THEN
            NO.ITEMS += 1
            RETURN.LIST<-1> = TEST.VAL<2>
         END
      NEXT
      
      IF RETURN.LIST = "" THEN
         CALL U_CRT.INFO("ERROR", "Could not find ":DQUOTE(IN.LIST.PREFIX):" in ":ID.PROPERTIES)
      END
   END ELSE
      CALL U_CRT.INFO("ERROR", "Not enough info to read list ":IN.PROPERTIES.FILE:"|":IN.LIST.PREFIX:"|":IN.LIST.NAME)
   END
   RETURN
END
