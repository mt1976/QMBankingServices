PROGRAM BCC
** INFORMATION ****************************************************************
*   Routine Name : BCC
*           Type : SUBROUTINE
*         Params :
*            Loc : UTIL.BP
** AUDIT **********************************************************************
*   Info Updated : 20210115 at 12.52.27 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE UTIL.BP I_UTIL.H

   CALL U_INITIALISE("BCC")

   STOP.ON.ERROR = @TRUE
   ID.UTIL.CONFIG = "codeHeader.txt" ; HEADER.TXT = ""

   CALL U_IO.READ(FV.UTIL.CONFIG, ID.UTIL.CONFIG, HEADER.TXT, STOP.ON.ERROR, ERROR.TEXT)

   PNM="GIT"
   CALL U_START("BCC")
   CALL U_HEADER("COMPILE & CATALOG")
   CMD.STRING = CONVERT(SPACE(1), @AM, @SENTENCE)
   IF CMD.STRING<2> = "" OR CMD.STRING<3> = "" THEN
      CALL U_CRT.INFO("ERROR", "Parameters [BP FILE] [PROGRAM or *]")
      CALL U_STOP(PNM)
      STOP
   END

   DEL CMD.STRING<1>

   SOURCE.FILE = CMD.STRING<1>


   FN.SOURCE.FILE = SOURCE.FILE ; FV.SOURCE.FILE = ""
   CALL U_IO.OPENFILE(FN.SOURCE.FILE, FV.SOURCE.FILE, STOP.ON.ERROR, ERROR.TEXT)

   IF CMD.STRING<2> = "*" OR CMD.STRING="ALL" THEN


      STMT.EX = "SELECT ":SOURCE.FILE:" TO ":@USERNO
      CALL U_OS.EXECUTE(STMT.EX, @FALSE)
      PROCESS.LIST = ""
      * STRIP OUT INSERTS/HEADERS
      LOOP
         READNEXT ID.TEMP FROM @USERNO ELSE EXIT
         IF ID.TEMP[2]=".H" THEN
         END ELSE
            PROCESS.LIST<-1> = ID.TEMP
         END
      REPEAT
      FOR I = 1 TO DCOUNT(PROCESS.LIST, @AM)
         SOURCE.FILE.AND.RECORD = SOURCE.FILE:" ":PROCESS.LIST<I>
         SOURCE.RECORD.ID = PROCESS.LIST<I>
         GOSUB DO.IT
      NEXT I

   END ELSE
      SOURCE.RECORD.ID = CMD.STRING<2>
      CMD.STRING = CONVERT(@AM, SPACE(1), CMD.STRING)
      SOURCE.FILE.AND.RECORD = CMD.STRING
      GOSUB DO.IT


   END

   STOP

DO.IT:
   IF SOURCE.RECORD.ID[1, 1]<>"." THEN

      * CALL U_CRT.INFO("BCC-START", SOURCE.FILE.AND.RECORD)

* REBUILD HEADER INFORMATION
      CALL U_BUILD.INFO.HELPER(FN.SOURCE.FILE, FV.SOURCE.FILE, SOURCE.RECORD.ID, HEADER.TXT)

      STMTS = "" ; MSG.TXT = ""
      STMTS<-1> = "FORMAT ":SOURCE.FILE.AND.RECORD ; MSG.TXT<-1> = "LINT"
      STMTS<-1> = "BASIC ":SOURCE.FILE.AND.RECORD ; MSG.TXT<-1> = "COMPILE"
      STMTS<-1> = "CATALOG ":SOURCE.FILE.AND.RECORD ; MSG.TXT<-1> = "CATALOG"
      *    STMTS<-1> = "U_DOCUMENT.DO ":SOURCE.FILE.AND.RECORD ; MSG.TXT<-1> = "DOCUMENT"

      NO.ACTIONS = DCOUNT(STMTS, @AM)
      FOR K = 1 TO NO.ACTIONS
         CALL U_CRT.INFO("ACTION", MSG.TXT<K>)
         CALL U_OS.EXECUTE(STMTS<K>, @TRUE)
      NEXT K


      R.SOURCE.REC = ""
      CALL U_IO.READ(FV.SOURCE.FILE, SOURCE.RECORD.ID, R.SOURCE.REC, @TRUE, "")
      TEST.ROW = R.SOURCE.REC<1>  ;* SUBROUTINE/PROGRAM must be on the first line or QM will fail to compile
      MATCH.TXT = "as12312324454djiaospdjaisopdannjfe"

      *  CALL U_CRT.INFO("ROW1", TEST.ROW)
      DO.TYPE = ""
      DO.TYPE = CHANGE(TEST.ROW, " ", @AM)<1>

      *  CALL U_CRT.INFO("SOURCE.FILE", FN.SOURCE.FILE)
      *  CALL U_CRT.INFO("SOURCE.RECORD.ID", SOURCE.RECORD.ID)
      *  CALL U_CRT.INFO("DO.TYPE", DO.TYPE)
      IF DO.TYPE <> "" THEN
         CALL U_CRT.INFO("ACTION", "DOCUMENT")
         CALL U_DOCUMENT.DO.HELPER(FN.SOURCE.FILE, SOURCE.RECORD.ID, DO.TYPE, FV.SOURCE.FILE, "")
      END
      *    CALL U_CRT.INFO("BCC-STOP", SOURCE.FILE.AND.RECORD)
   END
   RETURN

END
