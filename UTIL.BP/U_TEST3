PROGRAM U_TEST3
** INFORMATION ****************************************************************
*   Routine Name : U_TEST3
*           Type : PROGRAM
*         Params :
*            Loc : UTIL.BP
** AUDIT **********************************************************************
*   Info Updated : 20210209 at 11.18.42 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INSERT UTIL.BP I_UTIL.H

   CALL U_INITIALISE("U_TEST3")

   CALL U_START("U_TEST3")
   CALL U_HEADER("U_TEST3")
*** OPENFILES STARTS
   FN.SIENA.TXN.TEMPLATES = "SIENA.TXN.TEMPLATES" ; FV.SIENA.TXN.TEMPLATES = ""
   CALL U_IO.OPENFILE(FN.SIENA.TXN.TEMPLATES, FV.SIENA.TXN.TEMPLATES, @FALSE, "")

   FP.SOURCE = "" ; FV.SOURCE = ""
   CALL U_IO.GET.PROPERTY("siena_demo", "fundsCheckSource", FP.SOURCE)
   CALL U_CRT.INFO("SOURCE", FP.SOURCE)
   CALL U_IO.OPENFILEPATH(FP.SOURCE, FV.SOURCE, @TRUE, "")

   FP.DESTINATION = "" ; FV.DESTINATION = ""
   CALL U_IO.GET.PROPERTY("siena_demo", "fundsCheckResponseDest", FP.DESTINATION)
   CALL U_CRT.INFO("DEST", FP.DESTINATION)
   CALL U_IO.OPENFILEPATH(FP.DESTINATION, FV.DESTINATION, @TRUE, "")
*** OPENFILES ENDS

*** GET TEMPLATES
   CALL U_IO.GET.PROPERTY("siena_demo", "fundsCheckResponseTemplate", ID.SIENA.TXN.TEMPLATES)
   CALL U_IO.READ(FV.SIENA.TXN.TEMPLATES, ID.SIENA.TXN.TEMPLATES, R.SIENA.TXN.TEMPLATES, @FALSE, "")
*** GET TEMPLATES END

   SOURCE.TAG = 1
   DEST.TAG = 2

   P.ARRAY = ""
   P.ARRAY<-1> = "NS1:OPERATION":@VM:"OPERATION"
   P.ARRAY<-1> = "NS1:BIC":@VM:"BIC"
   P.ARRAY<-1> = "NS1:ACC":@VM:"ACC"
   P.ARRAY<-1> = "NS1:CCY":@VM:"CCY"
   P.ARRAY<-1> = "NS1:AMOUNT":@VM:"AMOUNT"
   P.ARRAY<-1> = "NS1:XREF":@VM:"XREF"
   P.ARRAY<-1> = "NS1:SOURCE":@VM:"SOURCE"
   P.ARRAY.SIZE = DCOUNT(P.ARRAY, @AM)

   CALL U_GET.LIST.PATH(FV.SOURCE, PROCESS.LIST, NO.ITEMS)

   CALL U_CRT.INFO("FOUND", NO.ITEMS)

   FOR THIS.ITEM = 1 TO NO.ITEMS
      ID.SOURCE = PROCESS.LIST<THIS.ITEM>
      GOSUB PROCESS.REQUEST
   NEXT THIS.ITEM

   CALL U_STOP("")
   STOP

PROCESS.REQUEST:

   R.RESPONSE = R.SIENA.TXN.TEMPLATES
   ID.TEST = PROCESS.LIST<THIS.ITEM>
   CALL U_IO.READ(FV.SOURCE, ID.SOURCE, R.SOURCE, @FALSE, "")

   FOR I = 1 TO P.ARRAY.SIZE
      FIND.TAG = P.ARRAY<I, SOURCE.TAG>
      REPL.TAG = P.ARRAY<I, DEST.TAG>
      CALL U_XML.FIND(FIND.TAG, R.SOURCE, SUBS.VALUE)
      CALL U_WILDCARD.SUBSTITUTE(REPL.TAG, SUBS.VALUE, R.RESPONSE)
   NEXT I

* YEAR, MONTH, DAY, HOURS, MINS, SECS
   CALL U_WILDCARD.SUBSTITUTE("YYYY", @YEAR4, R.RESPONSE)
   CALL U_WILDCARD.SUBSTITUTE("MM", @MONTH, R.RESPONSE)
   CALL U_WILDCARD.SUBSTITUTE("DD", @DAY, R.RESPONSE)
   CALL U_WILDCARD.SUBSTITUTE("HH", OCONV(TIME(), "MTS.")[1, 2], R.RESPONSE)
   CALL U_WILDCARD.SUBSTITUTE("mm", OCONV(TIME(), "MTS.")[4, 2], R.RESPONSE)
   CALL U_WILDCARD.SUBSTITUTE("SS", OCONV(TIME(), "MTS.")[7, 2], R.RESPONSE)

   ID.RESPONSE = ""
   CALL U_GET.UUID(ID.RESPONSE)
   CALL U_CRT.INFO("PROCESSED", THIS.ITEM:"/":NO.ITEMS:" ":DQUOTE(FP.SOURCE:"/":ID.SOURCE))
   CALL U_IO.WRITE(FV.DESTINATION, ID.RESPONSE, R.RESPONSE, @FALSE, "")
   CALL U_IO.DELETE(FV.SOURCE, ID.SOURCE, @FALSE, "")

   RETURN


END
