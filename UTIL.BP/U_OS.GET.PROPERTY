SUBROUTINE U_OS.GET.PROPERTY(IN.PROPERTIES.FILE, IN.PROPERTIES, IN.PROPERTY.NAME, RETURN.VAL)
** INFORMATION ****************************************************************
* Routine Name   : U_OS.GET.PROPERTY
*         Type   : SUBROUTINE
*         Params : <<PARAMETERS>>
*         Loc    : UTIL.BP
* -----------------------------------------------------------------------------
* Info Updated   : 20191220 at 15.41.22 in MWT-QM-DEV by sales
*                : on ldn-srv-ubnt01 (Linux)
* -----------------------------------------------------------------------------
*
$INCLUDE UTIL.BP I_UTIL.H
$INCLUDE UTIL.BP I_UTIL.LOG.EVENT.H

   RETURN.VAL = ""
   STOP.ON.ERROR = @TRUE
   VERBOSE = @TRUE

   IF IN.PROPERTIES.FILE <> "" AND IN.PROPERTIES <> "" AND IN.PROPERTY.NAME <> "" THEN

      ID.PROPERTIES = IN.PROPERTIES:".properties"

      FN.PROPERTIES= IN.PROPERTIES.FILE ; FV.PROPERTIES = ""
      CALL U_OPENFILE(FN.PROPERTIES, FV.PROPERTIES, @TRUE, "")

      CALL U_READ.LOCAL(FV.PROPERTIES, ID.PROPERTIES, R.PROPERTIES, STOP.ON.ERROR, "")

      NO.PROPERTIES = DCOUNT(R.PROPERTIES, @AM)
      * CALL U_CRT.INFO("NO.PROPS",NO.PROPERTIES)
      FOR THIS.PROPERTY = 1 TO NO.PROPERTIES

         TEST.VAL = CONVERT(U_PROP_SEP, @AM, R.PROPERTIES<THIS.PROPERTY>)
         UP.TEST.PROPERTY = UPCASE(TEST.VAL<1>)
         UP.SEARCH.PROPERTY = UPCASE(IN.PROPERTY.NAME)

         *   CALL U_CRT.INFO("TEST",UP.TEST.PROPERTY)
         *   CALL U_CRT.INFO("SEARCH",UP.SEARCH.PROPERTY)

         IF UP.TEST.PROPERTY = UP.SEARCH.PROPERTY THEN
            RETURN.VAL = TRIM(TEST.VAL<2>, @AM, "A")
            RETURN.VAL = TRIM(RETURN.VAL, @VM, "A")
            RETURN.VAL = TRIM(RETURN.VAL, @SM, "A")
            RETURN.VAL = TRIM(RETURN.VAL, @TM, "A")
            RETURN.VAL = TRIM(RETURN.VAL, CHAR(10), "A")
            RETURN.VAL = TRIM(RETURN.VAL, CHAR(13), "A")
            *CALL U_CRT.INFO("PPTVAL",RETURN.VAL)
            *CALL U_CRT.INFO("PPTLEN",LEN(RETURN.VAL))
         END

      NEXT

      IF RETURN.VAL = "" THEN
         CALL U_CRT.INFO("ERROR", "Could not find ":DQUOTE(IN.PROPERTY.NAME):" in ":ID.PROPERTIES)
      END

   END ELSE
      CALL U_CRT.INFO("ERROR", "Not enough info to read properties ":IN.PROPERTIES.FILE:"|":IN.PROPERTIES:"|":IN.PROPERTY.NAME)
   END



   RETURN
END
