SUBROUTINE U_CRT(TEXT, ITEM.NAME, CALLING.PROCESS)
** INFORMATION ****************************************************************
*   Routine Name : U_CRT
*           Type : SUBROUTINE
*         Params : TEXT, ITEM.NAME, CALLING.PROCESS
*            Loc : UTIL.BP
** AUDIT **********************************************************************
*   Info Updated : 20210209 at 11.18.42 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE UTIL.BP I_UTIL.H


   IN.TEXT = TEXT
   TEXT = CONVERT(@AM, U_CRT_AM, TEXT)
   TEXT = CONVERT(@VM, U_CRT_VM, TEXT)
   TEXT = CONVERT(@SM, U_CRT_SM, TEXT)

   dateTimeFormat = "" ; outputType = ""; timeZone = "" ; csvChar = ","

   CALL U_IO.GET.PROPERTY("util", "outputMode", outputMode)
   CALL U_IO.GET.PROPERTY("util", "outputType", outputType)
   CALL U_IO.GET.PROPERTY("util", "csvChar", csvChar)
   CALL U_IO.GET.PROPERTY("util", "timeZone", timeZone)

   OUTPUT.DATE = ""
   CALL U_GET.EPOCH.DATE(OUTPUT.DATE)
   OUTPUT.TEXT = ""

   BEGIN CASE
      CASE outputMode = U_LOGMODE_log4j


         * 20191203 UTC 16:15:39.089 INFO  NetClientImpl Client connecting on [localhost/127.0.0.1:7397]..
         * 20191203 UTC 16:15:39.121 INFO  NetChannelHandler Channel open

         OUTPUT.TEXT := OUTPUT.DATE
         OUTPUT.TEXT := " ":outputType
         OUTPUT.TEXT := " ":CALLING.PROCESS
         OUTPUT.TEXT := " ":ITEM.NAME
         OUTPUT.TEXT := " ":TEXT

      CASE outputMode = U_LOGMODE_csv
         * "20191203 UTC 16:15:39.089","INFO","NetClientImpl Client connecting on [localhost/127.0.0.1:7397]"
         OUTPUT.TEXT := DQUOTE(OUTPUT.DATE)
         OUTPUT.TEXT := csvChar:DQUOTE(outputType)
         OUTPUT.TEXT := csvChar:DQUOTE(CALLING.PROCESS)
         OUTPUT.TEXT := csvChar:DQUOTE(ITEM.NAME)
         OUTPUT.TEXT := csvChar:DQUOTE(TEXT)

      CASE outputMode = U_LOGMODE_simple
         OUTPUT.TEXT := "[":@(U_BOLD_ON):CONVERT(":", "", OCONV(TIME(), "MTS")):@(U_BOLD_OFF)
         OUTPUT.TEXT := "|":@(U_BOLD_ON):CALLING.PROCESS"L#15":@(U_BOLD_OFF)
         OUTPUT.TEXT := "|":@(U_BOLD_ON):UPCASE(ITEM.NAME)"L#10":@(U_BOLD_OFF)
         OUTPUT.TEXT := "][":TEXT:"]"

      CASE 1
         OUTPUT.TEXT := "[":@(U_BOLD_ON):OUTPUT.DATE:@(U_BOLD_OFF)
         OUTPUT.TEXT := "|":@(U_BOLD_ON):outputType:@(U_BOLD_OFF)
         OUTPUT.TEXT := "|":@(U_BOLD_ON):CALLING.PROCESS"L#20":@(U_BOLD_OFF)
         OUTPUT.TEXT := "|":@(U_BOLD_ON):UPCASE(ITEM.NAME)"L#15":@(U_BOLD_OFF)
         OUTPUT.TEXT := "][":TEXT:"]"
   END CASE
   DISPLAY @(U_CLEAR_LINE):OUTPUT.TEXT

   TEXT = IN.TEXT

   RETURN
END
