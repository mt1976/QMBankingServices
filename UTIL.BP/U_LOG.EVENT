SUBROUTINE U_LOG.EVENT(LOG.DATA, R.UTIL.LOG.EVENT)
** INFORMATION ****************************************************************
* Routine Name   : U_LOG.EVENT
*         Type   : SUBROUTINE
*         Params : LOG.DATA, R.UTIL.LOG.EVENT
*         Loc    : UTIL.BP
** AUDIT **********************************************************************
* Info Updated   : 20200323 at 15.04.38 in MWT-QM-DEV by sales
*                : on ldn-srv-ubnt01 (Linux)
*******************************************************************************

$INCLUDE UTIL.BP I_UTIL.H
$INCLUDE UTIL.BP F_UTIL.LOG.EVENT.H


   IP4.ADDRESS = ""
   CALL U_GET.IP(IP4.ADDRESS)

   ID.UTIL.LOG.CMD = ""
   CALL U_GET.UUID(ID.UTIL.LOG.CMD)
   IF LOG.DATA <> "" THEN
      CALL U_IO.WRITE(FV.UTIL.LOG.CMD, ID.UTIL.LOG.CMD, LOG.DATA, STOP.ON.ERROR, ERROR.TEXT)
   END

   IF R.UTIL.LOG.EVENT<U_LOG_EVENT> = "" THEN R.UTIL.LOG.EVENT<U_LOG_EVENT> = "UNKOWN"

   R.UTIL.LOG.EVENT<U_LOG_DATE> = OCONV(DATE(), "DX")
   R.UTIL.LOG.EVENT<U_LOG_TIME> = OCONV(TIME(), "MTS.")
   R.UTIL.LOG.EVENT<U_LOG_USER> = @USER

   IF LOG.DATA <> "" THEN
      R.UTIL.LOG.EVENT<U_LOG_ID> = ID.UTIL.LOG.CMD
   END

   R.UTIL.LOG.EVENT<U_LOG_TTY> = @TTY
   R.UTIL.LOG.EVENT<U_LOG_USERNO> = @USERNO
   R.UTIL.LOG.EVENT<U_LOG_PATH> = @PATH
   R.UTIL.LOG.EVENT<U_LOG_WHO> = @WHO
   R.UTIL.LOG.EVENT<U_LOG_SENTENCE> = @SENTENCE
   R.UTIL.LOG.EVENT<U_LOG_IP> = IP4.ADDRESS
   R.UTIL.LOG.EVENT<U_LOG_HOST> = SYSTEM(1015)
   R.UTIL.LOG.EVENT<U_LOG_PLATFORM> = SYSTEM(1010)
   R.UTIL.LOG.EVENT<U_LOG_THREAD> = U_PROCESS.THREAD.ID

   CALLING.SUBR = ""
   CALL U_GET.CALLING.PROCESS.NAME(CALLING.SUBR, SYSTEM(1002), SYSTEM(33))
   R.UTIL.LOG.EVENT<U_LOG_PROCESS> = CALLING.SUBR

   CALL U_IO.WRITE(FV.UTIL.LOG.EVENT, ID.UTIL.LOG.CMD, R.UTIL.LOG.EVENT, STOP.ON.ERROR, ERROR.TEXT)

   RETURN
END
