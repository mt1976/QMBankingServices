SUBROUTINE U_LOG.EVENT(LOG.DATA, R.UTIL.LOG.EVENT)
** INFORMATION ****************************************************************
*   Routine Name : U_LOG.EVENT
*           Type : SUBROUTINE
*         Params : LOG.DATA, R.UTIL.LOG.EVENT
*            Loc : UTIL.BP
** AUDIT **********************************************************************
*   Info Updated : 20210203 at 22.55.58 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE UTIL.BP I_UTIL.H
$INCLUDE UTIL.BP F_UTIL.LOG.EVENT.H

* SLEEP FOR 1 MILLISECOND

   IP4.ADDRESS = ""
   CALL U_GET.IP(IP4.ADDRESS)

   ID.UTIL.LOG.EVENT = ""
   CALL U_GET.UUID(ID.UTIL.LOG.EVENT)
   IF R.UTIL.LOG.EVENT<U_LOG_EVENT> = "" THEN R.UTIL.LOG.EVENT<U_LOG_EVENT> = "UNKOWN"
   IF R.UTIL.LOG.EVENT<U_LOG_CMD> = "" THEN R.UTIL.LOG.EVENT<U_LOG_CMD> = R.UTIL.LOG.EVENT<U_LOG_MSG>
   IF R.UTIL.LOG.EVENT<U_LOG_MSG> = "" THEN R.UTIL.LOG.EVENT<U_LOG_MSG> = R.UTIL.LOG.EVENT<U_LOG_CMD>

   R.UTIL.LOG.EVENT<U_LOG_DATE> = OCONV(DATE(), "DX")
   U_LOG.SEQ.NO += 1
   R.UTIL.LOG.EVENT<U_LOG_TIME> = OCONV(TIME(), "MTS"):".":U_LOG.SEQ.NO'"0"R#5'
   R.UTIL.LOG.EVENT<U_LOG_USER> = @USER
   R.UTIL.LOG.EVENT<U_LOG_TTY> = @TTY
   R.UTIL.LOG.EVENT<U_LOG_USERNO> = @USERNO
   R.UTIL.LOG.EVENT<U_LOG_PATH> = @PATH
   R.UTIL.LOG.EVENT<U_LOG_WHO> = @WHO
   R.UTIL.LOG.EVENT<U_LOG_SENTENCE> = @SENTENCE
   R.UTIL.LOG.EVENT<U_LOG_IP> = IP4.ADDRESS
   R.UTIL.LOG.EVENT<U_LOG_HOST> = SYSTEM(1015)
   R.UTIL.LOG.EVENT<U_LOG_PLATFORM> = SYSTEM(1010)
   R.UTIL.LOG.EVENT<U_LOG_THREAD> = U_PROCESS.THREAD.ID

   CALLING.SUBR = ""
   CALL U_GET.CALLING.PROCESS.NAME(CALLING.SUBR, SYSTEM(1002), SYSTEM(33))
   R.UTIL.LOG.EVENT<U_LOG_PROCESS> = CALLING.SUBR

   CALL U_IO.WRITE(FV.UTIL.LOG.EVENT, ID.UTIL.LOG.EVENT, R.UTIL.LOG.EVENT, STOP.ON.ERROR, ERROR.TEXT)

   RETURN
END
