SUBROUTINE U_BUILDFILES.HELPER(PROCESS.FILE, INSERT.FILE, PREFIX)
** INFORMATION ****************************************************************
* Routine Name   : U_BUILDFILES.HELPER
*         Type   : SUBROUTINE
*         Params : PROCESS.FILE, INSERT.FILE, PREFIX
*         Loc    : UTIL.BP
* -----------------------------------------------------------------------------
* Info Updated   : 20200121 at 15.43.43 in MWT-QM-DEV by sales
*                : on ldn-srv-ubnt01 (Linux)
* -----------------------------------------------------------------------------
*
$INCLUDE UTIL.BP I_UTIL.H
$INCLUDE UTIL.BP F_UTIL.LOG.EVENT.H


*  CALL U_CRT.INFO("ENTER","U_BUILDFILES.HELPER")
*  CALL U_CRT.INFO("PROCESSING",DQUOTE(PROCESS.FILE):DQUOTE(INSERT.FILE))

   PNM = "" ; STOP.ON.ERROR = @TRUE ; ERROR.TEXT = ""

*   CALL U_CRT.INFO("OPEN",PROCESS.FILE)
   FN.PROCESS.FILE = PROCESS.FILE ; FV.PROCESS.FILE = ""
   CALL U_IO.OPENFILE(FN.PROCESS.FILE, FV.PROCESS.FILE, STOP.ON.ERROR, ERROR.TEXT)
* CALL U_CRT.INFO("OPEN",INSERT.FILE)
   FN.INSERT.FILE = INSERT.FILE ; FV.INSERT.FILE = ""
   CALL U_IO.OPENFILE(FN.INSERT.FILE, FV.INSERT.FILE, STOP.ON.ERROR, ERROR.TEXT)

   FN.UTIL.FILE.DEFINITIONS = "UTIL.FILE.DEFINITIONS" ; FV.UTIL.FILE.DEFINITIONS = ""
* CALL U_CRT.INFO("OPEN",FN.UTIL.FILE.DEFINITIONS)
   CALL U_IO.OPENFILE(FN.UTIL.FILE.DEFINITIONS, FV.UTIL.FILE.DEFINITIONS, STOP.ON.ERROR, ERROR.TEXT)
* CALL U_CRT.INFO("READ",PROCESS.FILE)
   CALL U_IO.READ(FV.UTIL.FILE.DEFINITIONS, PROCESS.FILE, R.FILE.DEFINITIONS, STOP.ON.ERROR, ERROR.TEXT)

   ID.INSERT.FILE = "F_":PROCESS.FILE:".H"
   DEL.STMT = "DELETE ":FN.INSERT.FILE:" ":ID.INSERT.FILE
   CALL U_OS.EXECUTE(DEL.STMT, @TRUE)
*   CALL U_CRT.INFO("DELETE",DEL.STMT)

   NEW.EQUATES = "*"
   NEW.EQUATES<-1> = "* AUTOGENERATED FIELD DEFINITIONS"
   NEW.EQUATES<-1> = "* FILE  : ":DQUOTE(FN.INSERT.FILE)
   NEW.EQUATES<-1> = "* NAME  : ":DQUOTE(ID.INSERT.FILE)
   NEW.EQUATES<-1> = "* NAME  : ":DQUOTE(PREFIX)
   NEW.EQUATES<-1> = "* BUILT : ":OCONV(DATE(), "D4"):" at ":OCONV(TIME(), "MTS.")
   NEW.EQUATES<-1> = "*"

   NO.FLDS = DCOUNT(R.FILE.DEFINITIONS, @AM)
   FOR THIS.FLD = 1 TO NO.FLDS
      THIS.ROW = R.FILE.DEFINITIONS<THIS.FLD>
      P.ROW = CONVERT("/", @AM, THIS.ROW)

*     CALL U_CRT.INFO("MESSAGE",P.ROW)
      FLD.NO = P.ROW<3>
      FLD.NAME = P.ROW<5>
      IF P.ROW<2> = "D" THEN
        NEW.EQUATES<-1> = "equate ":PREFIX:FLD.NAME:" to ":FLD.NO
      END
*     CALL U_CRT.INFO("DICT",PROCESS.FILE:DQUOTE(THIS.ROW))
      CALL U_BUILDFILES.DICT(PROCESS.FILE, THIS.ROW)

   NEXT THIS.FLD

* COMPILE ITYPES
IT.STMT = "COMPILE.DICT ":PROCESS.FILE
   CALL U_OS.EXECUTE(IT.STMT, @TRUE)

*   CALL U_CRT.INFO("COMPLETE","EQUATES")
   CALL U_CRT.RECORD(NEW.EQUATES)
   CALL U_IO.WRITE(FV.INSERT.FILE, ID.INSERT.FILE, NEW.EQUATES, STOP.ON.ERROR, ERROR.TEXT)

   R.EVENT                = ""
   R.EVENT<U_LOG_EVENT>   = "REBUILD"
   R.EVENT<U_LOG_CMD>     = DEL.STMT
   R.EVENT<U_LOG_ID>      = PROCESS.FILE
   R.EVENT<U_LOG_MSG>     = ID.INSERT.FILE
   R.EVENT<U_LOG_RETCD>   = ""
   R.EVENT<U_LOG_SUBR>    = "U_BUILDFILES.HELPER"
   R.EVENT<U_LOG_PROCESS> = "U_BUILDFILES"
   R.EVENT<U_LOG_FILE>    = PROCESS.FILE

   CALL U_LOG.EVENT("", R.EVENT)

   RETURN
END
