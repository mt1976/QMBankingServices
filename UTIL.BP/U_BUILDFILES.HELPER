SUBROUTINE U_BUILDFILES.HELPER(PROCESS.FILE, INSERT.FILE, PREFIX)
** INFORMATION ****************************************************************
*   Routine Name : U_BUILDFILES.HELPER
*           Type : SUBROUTINE
*         Params : PROCESS.FILE, INSERT.FILE, PREFIX
*            Loc : UTIL.BP
** AUDIT **********************************************************************
*   Info Updated : 20210227 at 15.13.35 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE UTIL.BP I_UTIL.H
$INCLUDE UTIL.BP F_UTIL.LOG.EVENT.H


*  CALL U_CRT.INFO("ENTER","U_BUILDFILES.HELPER")
*  CALL U_CRT.INFO("PROCESSING",DQUOTE(PROCESS.FILE):DQUOTE(INSERT.FILE))

   PNM = "" ; STOP.ON.ERROR = @TRUE ; ERROR.TEXT = ""

*   CALL U_CRT.INFO("OPEN",PROCESS.FILE)
   FN.PROCESS.FILE = PROCESS.FILE ; FV.PROCESS.FILE = ""
   CALL U_IO.OPENFILE(FN.PROCESS.FILE, FV.PROCESS.FILE, STOP.ON.ERROR, ERROR.TEXT)

   FN.PROCESS.FILE.DICT = "DICT ":PROCESS.FILE ; FV.PROCESS.FILE.DICT = ""
* CALL U_CRT.INFO("OPEN",FN.PROCESS.FILE.DICT)
   CALL U_IO.OPENFILE(FN.PROCESS.FILE.DICT, FV.PROCESS.FILE.DICT, STOP.ON.ERROR, ERROR.TEXT)

* CALL U_CRT.INFO("OPEN",INSERT.FILE)
   FN.INSERT.FILE = INSERT.FILE ; FV.INSERT.FILE = ""
   CALL U_IO.OPENFILE(FN.INSERT.FILE, FV.INSERT.FILE, STOP.ON.ERROR, ERROR.TEXT)

   FN.UTIL.FILE.DEFINITIONS = "UTIL.FILE.DEFINITIONS" ; FV.UTIL.FILE.DEFINITIONS = ""
* CALL U_CRT.INFO("OPEN",FN.UTIL.FILE.DEFINITIONS)
   CALL U_IO.OPENFILE(FN.UTIL.FILE.DEFINITIONS, FV.UTIL.FILE.DEFINITIONS, STOP.ON.ERROR, ERROR.TEXT)
* CALL U_CRT.INFO("READ",PROCESS.FILE)
   CALL U_IO.READ(FV.UTIL.FILE.DEFINITIONS, PROCESS.FILE, R.FILE.DEFINITIONS, STOP.ON.ERROR, ERROR.TEXT)

   ID.INSERT.FILE = "F_":PROCESS.FILE:".H"
   DEL.STMT = "DELETE ":FN.INSERT.FILE:" ":ID.INSERT.FILE
   CALL U_OS.EXECUTE(DEL.STMT, @TRUE)
*   CALL U_CRT.INFO("DELETE",DEL.STMT)

   ID.UTIL.CONFIG = "insertHeader.txt" ; R.UTIL.CONFIG = ""
   CALL U_IO.READ(FV.UTIL.CONFIG, ID.UTIL.CONFIG, R.UTIL.CONFIG, STOP.ON.ERROR, ERROR.TEXT)
   NEW.EQUATES = R.UTIL.CONFIG
   NEW.EQUATES = CHANGE(NEW.EQUATES, "<<FILENAME>>", PROCESS.FILE)
   NEW.EQUATES = CHANGE(NEW.EQUATES, "<<FILEID>>", ID.INSERT.FILE)
   NEW.EQUATES = CHANGE(NEW.EQUATES, "<<STORED>>", FN.INSERT.FILE)
   NEW.EQUATES = CHANGE(NEW.EQUATES, "<<DATE>>", OCONV(DATE(), "DX"))
   NEW.EQUATES = CHANGE(NEW.EQUATES, "<<TIME>>", OCONV(TIME(), "MTS."))
   NEW.EQUATES = CHANGE(NEW.EQUATES, "<<WHO>>", @WHO)
   NEW.EQUATES = CHANGE(NEW.EQUATES, "<<USER>>", @USER)
   NEW.EQUATES = CHANGE(NEW.EQUATES, "<<HOSTNAME>>", SYSTEM(1015))
   NEW.EQUATES = CHANGE(NEW.EQUATES, "<<PLATFORM>>", SYSTEM(1010))
   NEW.EQUATES = CHANGE(NEW.EQUATES, "<<YYYY>>", @YEAR4)
   NEW.EQUATES = CHANGE(NEW.EQUATES, "<<PREFIX>>", PREFIX)


   NEW.EQUATES<-1> = "equate ":PREFIX:"FILE.NAME to ":DQUOTE(PROCESS.FILE)
   NO.FLDS = DCOUNT(R.FILE.DEFINITIONS, @AM)
   FIELDS.LIST = ""
   FOR THIS.FLD = 1 TO NO.FLDS
      THIS.ROW = R.FILE.DEFINITIONS<THIS.FLD>
      P.ROW = CONVERT("/", @AM, THIS.ROW)

*     CALL U_CRT.INFO("MESSAGE",P.ROW)
      FLD.NO = P.ROW<3>
      FLD.NAME = P.ROW<5>
      LIST.PREFIX = SPACE(1)
      IF FIELDS.LIST = "" THEN
         LIST.PREFIX = ""
      END
      FIELDS.LIST := LIST.PREFIX:FLD.NAME
      IF P.ROW<2> = "D" THEN
         IF FLD.NO > 0 THEN
            NEW.EQUATES<-1> = "equate ":PREFIX:FLD.NAME:" to ":FLD.NO
         END
      END
*     CALL U_CRT.INFO("DICT",PROCESS.FILE:DQUOTE(THIS.ROW))
      CALL U_BUILDFILES.DICT(PROCESS.FILE, THIS.ROW)

   NEXT THIS.FLD

* GENERATE an PH item 'ALL' to list all fields
   R.DICT = ""
   R.DICT<1> = "PH"
   R.DICT<2> = FIELDS.LIST
   R.DICT<11> = "[AUTOGENERATED ":OCONV(DATE(), "D4"):" at ":OCONV(TIME(), "MTS."):"]"
* CALL U_CRT.RECORD(R.DICT)
   CALL U_IO.WRITE(FV.PROCESS.FILE.DICT, "ALL", R.DICT, STOP.ON.ERROR, ERROR.TEXT)

* COMPILE ITYPES
   IT.STMT = "COMPILE.DICT ":PROCESS.FILE
   CALL U_OS.EXECUTE(IT.STMT, @TRUE)

*   CALL U_CRT.INFO("COMPLETE","EQUATES")

   isBuildMachine = @FALSE
   CALL U_BUILD.MACHINE.CHECK(isbuildMachine)
   IF isBuildMachine THEN
      CALL U_IO.WRITE(FV.INSERT.FILE, ID.INSERT.FILE, NEW.EQUATES, STOP.ON.ERROR, ERROR.TEXT)
   END

   R.EVENT = ""
   R.EVENT<U_LOG_EVENT> = "REBUILD"
   R.EVENT<U_LOG_CMD> = DEL.STMT
   R.EVENT<U_LOG_ID> = PROCESS.FILE
   R.EVENT<U_LOG_MSG> = ID.INSERT.FILE
   R.EVENT<U_LOG_RETCD> = ""
   R.EVENT<U_LOG_SUBR> = "U_BUILDFILES.HELPER"
   R.EVENT<U_LOG_PROCESS> = "U_BUILDFILES"
   R.EVENT<U_LOG_FILE> = PROCESS.FILE

   CALL U_LOG.EVENT("", R.EVENT)

   RETURN
END
