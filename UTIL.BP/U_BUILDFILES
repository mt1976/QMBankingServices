PROGRAM U_BUILDFILES
** INFORMATION ****************************************************************
*   Routine Name : U_BUILDFILES
*           Type : PROGRAM
*         Params :
*            Loc : UTIL.BP
** AUDIT **********************************************************************
*   Info Updated : 20210214 at 22.36.32 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE UTIL.BP I_UTIL.H
   CALL U_INITIALISE("U_BUILDFILES")
   PNM = "U_BUILDFILES" ; STOP.ON.ERROR = @TRUE ; ERROR.TEXT = ""
   CALL U_START(PNM)
   CALL U_HEADER("(RE)BUILD FILE(S)")

   FN.UTIL.FILE.DEFINITIONS = "UTIL.FILE.DEFINITIONS" ; FV.UTIL.FILE.DEFINITIONS = ""
   CALL U_IO.OPENFILE(FN.UTIL.FILE.DEFINITIONS, FV.UTIL.FILE.DEFINITIONS, STOP.ON.ERROR, ERROR.TEXT)

   CMD.STRING = CONVERT(SPACE(1), @AM, @SENTENCE)
   IF CMD.STRING<2> = "" THEN
      CALL U_CRT.INFO("ERROR", "Parameters [ALL, or specific i.e. UTIL/SIENA]")
      CALL U_STOP(PNM)
      STOP
   END

   * remove the program name from the stack of cmds
   DEL CMD.STRING<1>
   * loop through the cmd strings
   NO.CMDS = DCOUNT(CMD.STRING, @AM)

*   CALL U_CRT.INFO("NO PROCESSES", NO.CMDS)
   FOR THIS.CMD = 1 TO NO.CMDS
      DELIVERY.TYPE = CMD.STRING<THIS.CMD>
*      CALL U_CRT.INFO("PROCESS", DELIVERY.TYPE)
      STMT = "SELECT ":FN.UTIL.CONFIG:" LIKE build.":DOWNCASE(DELIVERY.TYPE):".properties"
      IF DELIVERY.TYPE = "ALL" THEN
         STMT = "SELECT ":FN.UTIL.CONFIG:" LIKE build":"...":"properties"
      END
      GOSUB PROCESS.IT
   NEXT THIS.CMD

   CALL U_STOP(PNM)

   STOP "DONE"

PROCESS.IT:
*CALL U_CRT.INFO('STMT',STMT)

   CALL U_OS.EXECUTE(STMT, @TRUE)

   PROCESS.LIST = ""
   LOOP
      READNEXT ID.FILE ELSE EXIT
      PROCESS.LIST<-1> = ID.FILE
   REPEAT

   NO.PROPERTIES.FILES = DCOUNT(PROCESS.LIST, @AM)
*CALL U_CRT.INFO("NO PROPERTIES", NO.PROPERTIES.FILES)
*CALL U_CRT.RECORD(PROCESS.LIST)

   FOR THIS.PROPERTIES.FILE = 1 TO NO.PROPERTIES.FILES
      *CALL U_CRT.INFO("PROPERTIES", PROCESS.LIST<THIS.PROPERTIES.FILE>)
      R.UTIL.CONFIG = ""
      CALL U_IO.READ(FV.UTIL.CONFIG, PROCESS.LIST<THIS.PROPERTIES.FILE>, R.UTIL.CONFIG, STOP.ON.ERROR, ERROR.TEXT)
      NO.FILES.TO.REBUILD = DCOUNT(R.UTIL.CONFIG, @AM)
      *CALL U_CRT.INFO("FILES", NO.FILES.TO.REBUILD)
      *CALL U_CRT.RECORD(R.UTIL.CONFIG)
      PREFIX=""
      FOR THIS.FILE = 1 TO NO.FILES.TO.REBUILD

         PROCESS.ITEM = CONVERT(U_PROP_SEP, @AM, R.UTIL.CONFIG<THIS.FILE>)
         PROCESS.WHAT = PROCESS.ITEM<1>
         PROCESS.FILE = PROCESS.ITEM<2>
         IF PROCESS.WHAT = "PREFIX" THEN
            PREFIX=PROCESS.FILE
         END
         IF PROCESS.WHAT = "FILE" THEN
            CALL U_CRT.INFO("REBUILD", PROCESS.FILE)
            PF = PROCESS.LIST<THIS.PROPERTIES.FILE>
            INSERT.FILE = UPCASE(CONVERT(".", @AM, PF)<2>):".BP"
            IF PF<>"SIENA" THEN INSERT.FILE = "UTIL.BP"
            *CALL U_CRT.INFO("IN", INSERT.FILE)
            CALL U_BUILDFILES.HELPER(PROCESS.FILE, INSERT.FILE, PREFIX)
         END

      NEXT THIS.FILE

   NEXT THIS.PROPERTIES.FILE
   RETURN
END
