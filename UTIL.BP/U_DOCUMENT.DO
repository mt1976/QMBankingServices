PROGRAM U_DOCUMENT.DO
** INFORMATION ****************************************************************
*   Routine Name : U_DOCUMENT.DO
*           Type : PROGRAM
*         Params :
*            Loc : UTIL.BP
** AUDIT **********************************************************************
*   Info Updated : 20210214 at 22.36.32 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE UTIL.BP I_UTIL.H

* USAGE U_DOCUMENT.DO <<Parameters>>
* USAGE Parameters [A specific file i.e. UTIL.BP]

   PROCESS.NAME = "U_DOCUMENT.DO"
   CALL U_INITIALISE(PROCESS.NAME)
   CALL U_START(PROCESS.NAME)
   CALL U_HEADER(PROCESS.NAME)

   CMD.STRING = CONVERT(SPACE(1), @AM, @SENTENCE)
   IF CMD.STRING<2> = "" THEN
      CALL U_CRT.INFO("ERROR", "Parameters [A specific file i.e. UTIL.BP]")
      CALL U_STOP(PROCESS.NAME)
      STOP
   END
* remove the program name from the stack of cmds
   DEL CMD.STRING<1>
* loop through the cmd strings
   THIS.CMD = 1

   DO.FILE = CMD.STRING<THIS.CMD>
   CALL U_CRT.INFO("PROCESS", DO.FILE)

   DOC_THEME = "" ; DOC_THEME_PATH = ""
   CALL U_IO.GET.PROPERTY("docs", "themeName", DOC_THEME)
   CALL U_IO.GET.PROPERTY("docs", "themeLocation", DOC_THEME_PATH)
   DOC_THEME_PATH = "../":DOC_THEME_PATH:"/"

   DOC_RES_BACK = ""
   DOC_RES_LINK = ""
   DOC_RES_APP = ""

   CALL U_IO.GET.PROPERTY(DOC_THEME, "docBack", DOC_RES_BACK)
   CALL U_IO.GET.PROPERTY(DOC_THEME, "docLink", DOC_RES_LINK)
   CALL U_IO.GET.PROPERTY(DOC_THEME, "applicationIcon", DOC_RES_APP)

   DOC_RES_BACK = DOC_THEME_PATH:DOC_THEME:"/":DOC_RES_BACK
   DOC_RES_LINK = DOC_THEME_PATH:DOC_THEME:"/":DOC_RES_LINK
   DOC_RES_APP = DOC_THEME_PATH:DOC_THEME:"/":DOC_RES_APP


   FN.FILENAME = DO.FILE
   FV.FILEVAR = ""

   CALL U_IO.OPENFILE(FN.FILENAME, FV.FILEVAR, @TRUE, "")


   FN.DOCS = "" ; FN.DOCS.PAGES = ""
   CALL U_IO.GET.PROPERTY("docs", "root", FN.DOCS)

   FV.DOCS = ""
   CALL U_IO.OPENFILE(FN.DOCS, FV.DOCS, @TRUE, "")

   PROCESS.LIST = ""
   TXT.INFORMATION = "" ; TXT.INSERTS = "" ; TXT.PROGRAMS = "" ; TXT.SUBR = MD$SUBR;TXT.FOOTER=""
   CALL U_TRANSLATE("docs", "INFORMATION", 1, TXT.INFORMATION)
   CALL U_TRANSLATE("docs", "INSERTS", 1, TXT.INSERTS)
   CALL U_TRANSLATE("docs", "PROGRAMS", 1, TXT.PROGRAMS)
   CALL U_TRANSLATE("docs", MD$SUBR, 1, TXT.SUBR)
   CALL U_TRANSLATE("docs", "FOOTER", 1, TXT.FOOTER)


   INFO.DOC = TXT.INFORMATION:" ":FN.FILENAME:MD$BR
   DETAIL.DOC = ""
   FOOTER.DOC = TXT.FOOTER:" ":FN.FILENAME:" ":@USER:MD$BR
   OUTPUT.DOC = ""
   DO.TYPE = ""

* INSERTS
* PROGRAMS

   DETAIL.DOC<-1> = MD$H1:TXT.INSERTS
   STMT.EX = "SSELECT ":FN.FILENAME:" WITH @ID LIKE I_...H OR WITH @ID LIKE F_...H"
   CALL U_OS.EXECUTE(STMT.EX, @TRUE)

   LOOP
      READNEXT DO.ITEM ELSE EXIT

      CALL U_DOCUMENT.DO.HELPER(FN.FILENAME, DO.ITEM, MD$HEADER, FV.FILEVAR, DETAIL.DOC)

   REPEAT

   DETAIL.DOC<-1> = MD$H1:TXT.PROGRAMS
   STMT.EX = "SSELECT ":FN.FILENAME:" WITH @ID UNLIKE ...H AND WITH PARAMETERS LIKE ...PROGRAM..."
   CALL U_OS.EXECUTE(STMT.EX, @TRUE)

   LOOP
      READNEXT DO.ITEM ELSE EXIT
      CALL U_DOCUMENT.DO.HELPER(FN.FILENAME, DO.ITEM, MD$PRG, FV.FILEVAR, DETAIL.DOC)
   REPEAT

   SUBR.TEXT = MD$SUBR
* Used to get around framework issue.
   DETAIL.DOC<-1> = MD$H1:TXT.SUBR

   STMT.EX = "SSELECT ":FN.FILENAME:" WITH @ID UNLIKE ...H AND WITH PARAMETERS LIKE ...":MD$SUBR:"..."
   CALL U_OS.EXECUTE(STMT.EX, @TRUE)

   LOOP
      READNEXT DO.ITEM ELSE EXIT
      CALL U_DOCUMENT.DO.HELPER(FN.FILENAME, DO.ITEM, MD$SUBR, FV.FILEVAR, DETAIL.DOC)
   REPEAT


   OUTPUT.DOC = INFO.DOC:@AM:DETAIL.DOC:@AM:FOOTER.DOC

   * CALL U_CRT.RECORD(OUTPUT.DOC)
   ID.RECORD = FN.FILENAME:MD$SUFFIX
   CALL U_IO.WRITE(FV.DOCS, ID.RECORD, OUTPUT.DOC, @TRUE, "")

   CALL U_STOP(PROCESS.NAME)

END
