SUBROUTINE U_STOP(PROGRAM.NAME)
** INFORMATION ****************************************************************
* Routine Name   : U_STOP
*         Type   : SUBROUTINE
*         Params : PROGRAM.NAME
*         Loc    : UTIL.BP
** AUDIT **********************************************************************
* Info Updated   : 20200609 at 19.09.05 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INSERT UTIL.BP I_UTIL.H
$INSERT UTIL.BP F_UTIL.LOG.EVENT.H

   STMT = "COMO OFF"
   HUSH ON
   EXECUTE STMT
   HUSH OFF


   IF PROGRAM.NAME <> "" THEN

      FN.COMO = "$COMO" ; FV.COMO = "" ; STOP.ON.ERROR = @TRUE
      CALL U_IO.OPENFILE(FN.COMO, FV.COMO, @TRUE, "")

      FN.UTIL.LOG = "UTIL.LOG" ; FV.UTIL.LOG = ""
      CALL U_IO.OPENFILE(FN.UTIL.LOG, FV.UTIL.LOG, STOP.ON.ERROR, "")

      R.RECORD = ""
      CALL U_IO.READ(FV.COMO, PROGRAM.NAME, R.RECORD, STOP.ON.ERROR, ERROR.TEXT)
      OUTPUT.NAME = PROGRAM.NAME:".log"
      CALL U_IO.WRITE(FV.UTIL.LOG, OUTPUT.NAME, R.RECORD, STOP.ON.ERROR, ERROR.TEXT)
      CALL U_IO.DELETE(FV.COMO, PROGRAM.NAME, STOP.ON.ERROR, ERROR.TEXT)

      END.TIME = TIME()

      R.EVENT = ""
      R.EVENT<U_LOG_EVENT> = "TIMING"
      R.EVENT<U_LOG_CMD> = ""
      R.EVENT<U_LOG_ID> = OUTPUT.NAME
      R.EVENT<U_LOG_MSG> = PROGRAM.NAME
      R.EVENT<U_LOG_RETCD> = ""
      R.EVENT<U_LOG_SUBR> = "U_STOP"
      R.EVENT<U_LOG_START> = OCONV(U_PROCESS.START.TIME, "MTS.")
      R.EVENT<U_LOG_STOP> = OCONV(END.TIME, "MTS.")
      R.EVENT<U_LOG_ELAPSED> = OCONV((END.TIME-U_PROCESS.START.TIME), "MTS.")

      CALLING.SUBR = ""
      CALL U_GET.CALLING.PROCESS.NAME(CALLING.SUBR, SYSTEM(1002), SYSTEM(33))
      R.EVENT<U_LOG_PROCESS> = CALLING.SUBR

      CALL U_LOG.EVENT("", R.EVENT)
      CRT "!INFO - Done ":DQUOTE(PROGRAM.NAME)

   END

   RETURN
END
