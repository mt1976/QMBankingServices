SUBROUTINE U_OS.WGET(URI, VERBOSE, RESULT)
** INFORMATION ****************************************************************
*   Routine Name : U_OS.WGET
*           Type : SUBROUTINE
*         Params : URI, VERBOSE, RESULT
*            Loc : UTIL.BP
** AUDIT **********************************************************************
*   Info Updated : 20210122 at 13.03.23 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE UTIL.BP I_UTIL.H
$INCLUDE UTIL.BP F_UTIL.LOG.EVENT.H


* GOOGLE CHROME FETCH
* -------------------
* Uses a headless google-chrome instance to scrap fully rendered html & data from a uri
* the result is stored and returned to the calling process.

* Get the location of the chrome executable;
   WGET.LOCATION = "" ; WGET.DEFAULTS = "" ; WGET.TEMP = ""
   STOP.ON.ERROR = @FALSE ; ERROR.TEXT = ""
   CALL U_IO.GET.PROPERTY("util", "wgetLocation", WGET.LOCATION)
   CALL U_IO.GET.PROPERTY("util", "wgetDefaults", WGET.DEFAULTS)
   CALL U_IO.GET.PROPERTY("util", "wgetTemp", WGET.TEMP)
   WGET.TEMP = WGET.TEMP:"/"

   CALL U_CRT.INFO("LOCATION", WGET.LOCATION)
   CALL U_CRT.INFO("DEFAULTS", WGET.DEFAULTS)
   CALL U_CRT.INFO("TEMP", WGET.TEMP)


   CALL U_IO.OPENFILEPATH(WGET.TEMP, FV.TEMP.FILE, STOP.ON.ERROR, ERROR.TEXT)


   TIME.START = TIME()

   CALL U_CRT.INFO("START", OCONV(TIME.START, "MTS."))
   CALL U_CRT.INFO("URI", URI)

   *HUSH ON
   CALL U_GET.UUID(ID.TEMP)
   ID.TEMP.LOC=WGET.TEMP:ID.TEMP

   STMT = WGET.LOCATION:SPACE(1):"-O ":DQUOTE(ID.TEMP.LOC):SPACE(1):WGET.DEFAULTS:SPACE(1):URI

   CALL U_CRT.INFO("COMMAND", @(U_BOLD_ON):STMT:@(U_BOLD_OFF))

   OS.EXECUTE STMT CAPTURING RESULT

   CALL U_IO.READ(FV.TEMP.FILE, ID.TEMP, RESULT, STOP.ON.ERROR, ERROR.TEXT)

   RESULT = CHANGE(RESULT, CHAR(10), "")
   RESULT = CHANGE(RESULT, @AM, "")
   RESULT = TRIM(CHANGE(RESULT, CHAR(13), ""), " ")

   TIME.STOP = TIME()
   TIME.ELAPSED = TIME.STOP - TIME.START

   CALL U_CRT.INFO("STOP", OCONV(TIME.START, "MTS."))
   CALL U_CRT.INFO("ELAPSED", @(U_BOLD_ON):OCONV(TIME.ELAPSED, "MTS."):@(U_BOLD_OFF))


   R.EVENT = ""
   R.EVENT<U_LOG_EVENT> = "WGET"
   R.EVENT<U_LOG_CMD> = STMT
   R.EVENT<U_LOG_MSG> = URI
   R.EVENT<U_LOG_URI> = URI
   R.EVENT<U_LOG_RETCD> = @SYSTEM.RETURN.CODE
   R.EVENT<U_LOG_SUBR> = "U_WGET"
   R.EVENT<U_LOG_START> = OCONV(TIME.START, "MTS.")
   R.EVENT<U_LOG_STOP> = OCONV(TIME.STOP, "MTS.")
   R.EVENT<U_LOG_ELAPSED> = OCONV(TIME.ELAPSED, "MTS.")

   CALL U_LOG.EVENT(RESULT, R.EVENT)

   RETURN
END
