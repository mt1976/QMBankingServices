SUBROUTINE SIENA.DELIVER.HELPER(PROCESS.NAME, SPECIFIC)
** INFORMATION ****************************************************************
* Routine Name   : SIENA.DELIVER.HELPER
*         Type   : SUBROUTINE
*         Loc    : SIENA.BP
* -----------------------------------------------------------------------------
* Info Updated   : 20191219 at 14.47.26 in MWT-QM-DEV by sales
*                : on ldn-srv-ubnt01 (Linux)
* -----------------------------------------------------------------------------
*
*******************************************************************************
$INCLUDE UTIL.BP I_UTIL.H

* INITIALISE
   CALL U_START(PROCESS.NAME)
* Setup some temp locations for input data

   ERROR.TEXT = ""; STOP.ON.ERROR = @TRUE ; VERBOSE = @TRUE
* OPEN FILES

   FN.SIENA.STAGING = "SIENA.STAGING" ; FV.SIENA.STAGING = ""
   CALL U_OPENFILE(FN.SIENA.STAGING, FV.SIENA.STAGING, STOP.ON.ERROR, ERROR.TEXT)
   FN.SIENA.CONFIG = "SIENA.CONFIG" ; FV.SIENA.CONFIG = ""
   CALL U_OPENFILE(FN.SIENA.CONFIG, FV.SIENA.CONFIG, STOP.ON.ERROR, ERROR.TEXT)

   CALL U_HEADER("SIENA - Deliver Data ":DQUOTE(SPECIFIC))

   CALL U_READ(FV.SIENA.CONFIG, "delivery.list", R.SIENA.CONFIG, STOP.ON.ERROR, ERROR.TEXT)

   * CALL U_CRT.RECORD(R.SIENA.CONFIG)

   NO.ITERATIONS = DCOUNT(R.SIENA.CONFIG, @AM)
   CALL U_CRT.INFO("NO CONFIGS", NO.ITERATIONS)
   FOR THIS.ITERATION = 1 TO NO.ITERATIONS

      SIENA.RVID = ""
      SIENA.DROPOFF = ""

      INSTRUCTION = ""
      INSTRUCTION = CONVERT(U_PROP_SEP, @AM, R.SIENA.CONFIG<THIS.ITERATION>)

      SIENA.RVID = INSTRUCTION<1>
      SIENA.DROPOFF = INSTRUCTION<2>
      IF SIENA.RVID # "#" THEN
         IF (SPECIFIC # "" AND SIENA.RVID = SPECIFIC) OR (SPECIFIC="") THEN
            CALL U_CRT.INFO("DELIVERING", @(U_BOLD_ON):SIENA.DROPOFF:@(U_BOLD_OFF))
            CALL SIENA.DELIVER.FILE(SIENA.RVID, SIENA.DROPOFF)
         END
      END
   NEXT THIS.ITERATION

   CALL U_STOP(PROCESS.NAME)

   RETURN
END
