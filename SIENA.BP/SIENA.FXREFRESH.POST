SUBROUTINE SIENA.FXREFRESH.POST(PROCESS.NAME)
** INFORMATION ****************************************************************
* Routine Name   : SIENA.FXREFRESH.HELPER
*         Type   : SUBROUTINE
*         Params : PROCESS.NAME, SPECIFIC
*         Loc    : SIENA.BP
* -----------------------------------------------------------------------------
* Info Updated   : 20200121 at 15.43.53 in MWT-QM-DEV by sales
*                : on ldn-srv-ubnt01 (Linux)
* -----------------------------------------------------------------------------
*
$INCLUDE UTIL.BP I_UTIL.H
$INCLUDE UTIL.BP F_UTIL.LOG.EVENT.H
* INITIALISE
   CALL U_START(PROCESS.NAME)
* Setup some temp locations for input data

   FN.SIENA.TEMP = "SIENA.TEMP" ; FV.SIENA.TEMP = "" ; STOP.ON.ERROR = @TRUE ; ERROR.TEXT = ""
   CALL U_IO.OPENFILE(FN.SIENA.TEMP, FV.SIENA.TEMP, STOP.ON.ERROR, ERROR.TEXT)

   ERROR.TEXT = ""; STOP.ON.ERROR = @TRUE ; VERBOSE = @TRUE

   ID.UTIL.CONFIG = "siena_ccyPair.list"

   CALL U_HEADER("Merge Temp CCY lists into master")

   GOSUB UPDATE.LIST

   RETURN

UPDATE.LIST:
   STMT.EX = "SELECT ":FN.SIENA.TEMP:" WITH @ID LIKE tmp_..._":ID.UTIL.CONFIG
   CALL U_OS.EXECUTE(STMT.EX, @FALSE)
   PROCESS.LIST = ""
   LOOP
      READNEXT ID.SIENA.TEMP ELSE EXIT
      PROCESS.LIST<-1> = ID.SIENA.TEMP
   REPEAT

   RES.COUNT = DCOUNT(PROCESS.LIST, @AM)

   CALL U_CRT.INFO("NO RECORDS", RES.COUNT)

   R.NEW.LIST = ""
   R.NEW.LIST<-1> = "#=----------------------------------------------------------------------------="
   R.NEW.LIST<-1> = "#=AUTO GENERATED - FX SPOT & FORWARD CURVES TO FETCH="
   R.NEW.LIST<-1> = "#=GENERATED at ":OCONV(TIME(), "MTS."):" on ":OCONV(DATE(), "D4/"):"="
   R.NEW.LIST<-1> = "#=Temp file is ":ID.UTIL.CONFIG:" in UTIL.CONFIG="
   R.NEW.LIST<-1> = "#=----------------------------------------------------------------------------="

   R.SORT.LIST = ""

   FOR THIS.ITEM.POS = 1 TO RES.COUNT
      ID.SIENA.TEMP = PROCESS.LIST<THIS.ITEM.POS>
      CALL U_IO.READ(FV.SIENA.TEMP, ID.SIENA.TEMP, R.SIENA.TEMP, STOP.ON.ERROR, ERROR.TEXT)
      IF R.SIENA.TEMP <> "" THEN
         IF R.SORT.LIST = "" THEN
            R.SORT.LIST = R.SIENA.TEMP
         END ELSE
            R.SORT.LIST := @AM:R.SIENA.TEMP
         END
      END
   NEXT THIS.ITEM.POS

   SORTED.LIST = ""
   CALL !SORT(R.SORT.LIST, SORTED.LIST, "AU")

   R.NEW.LIST<-1> := @AM:SORTED.LIST

   CALL U_IO.WRITE(FV.UTIL.CONFIG, ID.UTIL.CONFIG, R.NEW.LIST, STOP.ON.ERROR, ERROR.TEXT)

   R.UTIL.LOG.EVENT = ""
   R.UTIL.LOG.EVENT<U_LOG_EVENT> = 'REGENERATE'
   R.UTIL.LOG.EVENT<U_LOG_MSG> = 'UTIL.CONFIG UPDATED'
   R.UTIL.LOG.EVENT<U_LOG_ID> = ID.UTIL.CONFIG

   CALL U_CRT.RECORD(R.NEW.LIST)
   CALL U_LOG.EVENT("", R.UTIL.LOG.EVENT)

   RETURN

END
