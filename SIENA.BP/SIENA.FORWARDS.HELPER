SUBROUTINE SIENA.FORWARDS.HELPER(PROCESS.NAME)
** INFORMATION ****************************************************************
* Routine Name   : SIENA.FORWARDS.HELPER
*         Type   : SUBROUTINE
*         Params : <<PARAMETERS>>
*         Loc    : SIENA.BP
* -----------------------------------------------------------------------------
* Info Updated   : 20191220 at 15.41.38 in MWT-QM-DEV by sales
*                : on ldn-srv-ubnt01 (Linux)
* -----------------------------------------------------------------------------
*
$INCLUDE UTIL.BP I_UTIL.TRANSLATE.H
$INCLUDE UTIL.BP I_RATECSV.H
$INCLUDE UTIL.BP I_UTIL.H

* INITIALISE
   CALL U_START(PROCESS.NAME)
* Setup some temp locations for input data


   ERROR.TEXT = ""; STOP.ON.ERROR = @TRUE ; VERBOSE = @TRUE
   RVDATA = ""
* OPEN FILES
   FN.SIENA.IN = "SIENA.IN" ; FV.SIENA.IN = ""
   CALL U_OPENFILE(FN.SIENA.IN, FV.SIENA.IN, STOP.ON.ERROR, ERROR.TEXT)
   FN.SIENA.PROCESSED = "SIENA.PROCESSED" ; FV.SIENA.PROCESSED = ""
   CALL U_OPENFILE(FN.SIENA.PROCESSED, FV.SIENA.PROCESSED, STOP.ON.ERROR, ERROR.TEXT)
   FN.SIENA.STAGING = "SIENA.STAGING" ; FV.SIENA.STAGING = ""
   CALL U_OPENFILE(FN.SIENA.STAGING, FV.SIENA.STAGING, STOP.ON.ERROR, ERROR.TEXT)

   CALL U_HEADER("SIENA - FX FORWARDS")

   URI.PREFIX = "" ; URI.SUFFIX = "" ; RVTYPE = "" ; RVFRIG = ""
   CALL U_OS.GET.PROPERTY("SIENA.CONFIG", "forwards", "uriPrefix", URI.PREFIX)
   CALL U_OS.GET.PROPERTY("SIENA.CONFIG", "forwards", "uriSuffix", URI.SUFFIX)
   CALL U_OS.GET.PROPERTY("SIENA.CONFIG", "forwards", "RVType", RVTYPE)
   CALL U_OS.GET.PROPERTY("SIENA.CONFIG", "forwards", "RVFrig", RVFRIG)
   CALL U_OS.GET.PROPERTY("SIENA.CONFIG", "forwards", "precision", precisionValue)

   PRECISION precisionValue

   CCYPAIR.LIST = "" ; NO.PAIRS = -1
   CALL U_OS.GET.LIST("SIENA.CONFIG", "forwards", "X", CCYPAIR.LIST, NO.PAIRS)

   TENOR.LIST = "" ; NO.TENORS = -1
   CALL U_OS.GET.LIST("SIENA.CONFIG", "forward_tenors", "TENOR", TENOR.LIST, NO.TENORS)

   CALL U_CRT.INFO("NO PAIRS", NO.PAIRS)
   OUTPUT.DATA = ""

   FOR THIS.PAIR = 1 TO NO.PAIRS
      CURRENCY.PAIR = CCYPAIR.LIST<THIS.PAIR>
      CALL U_CRT.INFO("FETCH", CURRENCY.PAIR)
      STMT.URI = URI.PREFIX:CURRENCY.PAIR:URI.SUFFIX
      CALL U_CRT.INFO("URI", STMT.URI)
      CALL U_OS.CHROME.FETCH(STMT.URI, VERBOSE, RESULT)
      IF RESULT<> "" THEN
         GOSUB PROCESS.HTML
      END

   NEXT THIS.PAIR

   IF OUTPUT.DATA <> "" THEN
      ID.SIENA.STAGING = "RVMARKET":U_SEP:"BCFWD"
      CALL U_CRT.INFO("DELETE", ID.SIENA.STAGING)
      CALL U_DELETE(FV.SIENA.STAGING, ID.SIENA.STAGING, STOP.ON.ERROR, ERROR.TEXT)
      CALL U_CRT.INFO("STORE", ID.SIENA.STAGING)
      CALL U_WRITE(FV.SIENA.STAGING, ID.SIENA.STAGING, OUTPUT.DATA, STOP.ON.ERROR, ERROR.TEXT)
   END

   CALL U_CRT.INFO("PROCESSING", "COMPLETE")
   CALL U_STOP(PROCESS.NAME)

   RETURN

PROCESS.HTML:
* --------------------------
* WORKING ON CURRENCY PAIR
* --------------------------
*
* remove all @AM's we need this as one flat string


*CALL U_CRT.INFO("SEARCH", SEARCH.STRING)

   SEARCH.DATA = CHANGE(RESULT, CHAR(10), "")
   SEARCH.DATA = CHANGE(SEARCH.DATA, @AM, "")
   SEARCH.DATA = TRIM(CHANGE(SEARCH.DATA, CHAR(13), ""), " ")

   FOR THIS.TENOR = 1 TO NO.TENORS

      TENOR = TENOR.LIST<THIS.TENOR>
      CALL U_TRANSLATE("TENOR", TENOR, U_TRNS.BARCHARTCOM, SIENA.TENOR)
      SEARCH.STRING = TENOR:'</a>'
      SEARCH.DATA = CHANGE(SEARCH.DATA, SEARCH.STRING, @AM)
      PTS.SEARCH.STRING = '<span data-ng-bind="cell">'
      PTS.SEARCH.DATA = CHANGE(SEARCH.DATA<2>, PTS.SEARCH.STRING, @AM)
*CALL U_CRT.RECORD(PTS.SEARCH.DATA)
      SPLIT.STRING = '</span>'
      BID.STRING = CHANGE(PTS.SEARCH.DATA<2>, SPLIT.STRING, @AM)<1>
      ASK.STRING = CHANGE(PTS.SEARCH.DATA<3>, SPLIT.STRING, @AM)<1>
      IF NUM(BID.STRING) AND NUM(ASK.STRING) THEN
         * Frig the feed data - SIENA is multiplying by a factor of 1000
         BID.STRING=((BID.STRING+0)/RVFRIG)+0
         ASK.STRING=((ASK.STRING+0)/RVFRIG)+0
         GOSUB ADD.RVDATA
      END
*CALL U_CRT.INFO('TENOR',TENOR)
*CALL U_CRT.INFO('STNR',SIENA.TENOR)
*CALL U_CRT.INFO('BID',BID.STRING)
*CALL U_CRT.INFO('ASK',ASK.STRING)
      DEL SEARCH.DATA<1>
   NEXT THIS.TENOR

   RETURN
ADD.RVDATA:

   OUT.CCYTN = RVTYPE:CURRENCY.PAIR"L#6":SIENA.TENOR

   OUT.BUY = "D":BID.STRING"L#11"
   OUT.SEL = "D":ASK.STRING"L#11"
   ROW.DATA = OUT.CCYTN:OUT.BUY:OUT.SEL
   CALL U_CRT.INFO(CURRENCY.PAIR, ROW.DATA)
   OUTPUT.DATA<-1> = ROW.DATA
   RETURN
*-------------------------------------
END
