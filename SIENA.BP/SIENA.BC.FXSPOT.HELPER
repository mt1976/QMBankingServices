SUBROUTINE SIENA.BC.FXSPOT.HELPER(PROCESS.NAME)
** INFORMATION ****************************************************************
* Routine Name   : SIENA.BC.FXSPOT.HELPER
*         Type   : SUBROUTINE
*         Params : <<PARAMETERS>>
*         Loc    : SIENA.BP
* -----------------------------------------------------------------------------
* Info Updated   : 20191220 at 15.41.38 in MWT-QM-DEV by sales
*                : on ldn-srv-ubnt01 (Linux)
* -----------------------------------------------------------------------------
*
$INCLUDE UTIL.BP I_UTIL.TRANSLATE.H
$INCLUDE UTIL.BP I_RATECSV.H
$INCLUDE UTIL.BP I_UTIL.H

* INITIALISE
   CALL U_START(PROCESS.NAME)
* Setup some temp locations for input data


   ERROR.TEXT = ""; STOP.ON.ERROR = @TRUE ; VERBOSE = @TRUE
   RVDATA = ""
* OPEN FILES
   FN.SIENA.IN = "SIENA.IN" ; FV.SIENA.IN = ""
   CALL U_OPENFILE(FN.SIENA.IN, FV.SIENA.IN, STOP.ON.ERROR, ERROR.TEXT)
   FN.SIENA.PROCESSED = "SIENA.PROCESSED" ; FV.SIENA.PROCESSED = ""
   CALL U_OPENFILE(FN.SIENA.PROCESSED, FV.SIENA.PROCESSED, STOP.ON.ERROR, ERROR.TEXT)
   FN.SIENA.STAGING = "SIENA.STAGING" ; FV.SIENA.STAGING = ""
   CALL U_OPENFILE(FN.SIENA.STAGING, FV.SIENA.STAGING, STOP.ON.ERROR, ERROR.TEXT)

   CALL U_HEADER("SIENA - FX SPOT - BARCHART.COM")

   URI.PREFIX = "" ; URI.SUFFIX = "" ; RVTYPE = "" ; RVFRIG = ""
   CALL U_OS.GET.PROPERTY("SIENA.CONFIG", "forwards", "uriPrefix", URI.PREFIX)
   CALL U_OS.GET.PROPERTY("SIENA.CONFIG", "forwards", "uriSPSuffix", URI.SUFFIX)
   CALL U_OS.GET.PROPERTY("SIENA.CONFIG", "forwards", "RVType", RVTYPE)
   CALL U_OS.GET.PROPERTY("SIENA.CONFIG", "forwards", "RVFrig", RVFRIG)
   CALL U_OS.GET.PROPERTY("SIENA.CONFIG", "forwards", "precision", precisionValue)

   PRECISION precisionValue

   CCYPAIR.LIST = "" ; NO.PAIRS = -1
   CALL U_OS.GET.LIST("SIENA.CONFIG", "forwards", "X", CCYPAIR.LIST, NO.PAIRS)

   CALL U_CRT.INFO("NO PAIRS", NO.PAIRS)
   OUTPUT.DATA = ""

   FOR THIS.PAIR = 1 TO NO.PAIRS
      CURRENCY.PAIR = CCYPAIR.LIST<THIS.PAIR>
      CALL U_CRT.INFO("FETCH", CURRENCY.PAIR)
      STMT.URI = URI.PREFIX:CURRENCY.PAIR:URI.SUFFIX
      CALL U_CRT.INFO("URI", STMT.URI)
      CALL U_OS.CHROME.FETCH(STMT.URI, VERBOSE, RESULT)
      IF RESULT<> "" THEN
         GOSUB PROCESS.HTML
      END

   NEXT THIS.PAIR

   IF OUTPUT.DATA <> "" THEN
      ID.SIENA.STAGING = "RVMARKET":U_SEP_ALT:"BCSP"
      CALL U_CRT.INFO("DELETE", ID.SIENA.STAGING)
      CALL U_DELETE(FV.SIENA.STAGING, ID.SIENA.STAGING, STOP.ON.ERROR, ERROR.TEXT)
      CALL U_CRT.INFO("STORE", ID.SIENA.STAGING)
      CALL U_WRITE(FV.SIENA.STAGING, ID.SIENA.STAGING, OUTPUT.DATA, STOP.ON.ERROR, ERROR.TEXT)
   END

   CALL U_CRT.INFO("PROCESSING", "COMPLETE")
   CALL U_STOP(PROCESS.NAME)

   RETURN
*-------------------------------------
PROCESS.HTML:
* --------------------------
* WORKING ON CURRENCY PAIR
* --------------------------
*
* remove all @AM's we need this as one flat string
   ORIG.SEARCH.DATA = CHANGE(RESULT, CHAR(10), "")
   ORIG.SEARCH.DATA = CHANGE(ORIG.SEARCH.DATA, @AM, "")
   ORIG.SEARCH.DATA = TRIM(CHANGE(ORIG.SEARCH.DATA, CHAR(13), ""), " ")

   SEARCH.STRING = 'bidPrice&quot;:&quot;'
   GOSUB SEARCH.FOR.PRICE
   BID.STRING = GET.STRING

   SEARCH.STRING = 'askPrice&quot;:&quot;'
   GOSUB SEARCH.FOR.PRICE
   ASK.STRING = GET.STRING

   IF NUM(BID.STRING) AND NUM(ASK.STRING) THEN
      * Frig the feed data - SIENA is multiplying by a factor of 1000
      BID.STRING=(BID.STRING+0)
      ASK.STRING=(ASK.STRING+0)
      GOSUB ADD.RVDATA

   END


   RETURN
*-------------------------------------
ADD.RVDATA:
   OUT.CCYTN = 's':CURRENCY.PAIR"L#6":"spt"
   OUT.BUY = "D":BID.STRING"L#11"
   OUT.SEL = "D":ASK.STRING"L#11"
   ROW.DATA = OUT.CCYTN:OUT.BUY:OUT.SEL
   CALL U_CRT.INFO(CURRENCY.PAIR, ROW.DATA)
   OUTPUT.DATA<-1> = ROW.DATA
   RETURN

*-------------------------------------
SEARCH.FOR.PRICE:
   SEARCH.DATA = CHANGE(ORIG.SEARCH.DATA, SEARCH.STRING, @AM)
   PTS.SEARCH.STRING = "&quot;"
   PTS.SEARCH.DATA = CHANGE(SEARCH.DATA<2>, PTS.SEARCH.STRING, @AM)
   GET.STRING = PTS.SEARCH.DATA<1>
   RETURN
*-------------------------------------
END
