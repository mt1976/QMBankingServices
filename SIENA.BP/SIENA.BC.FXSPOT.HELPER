SUBROUTINE SIENA.BC.FXSPOT.HELPER(PROCESS.NAME)
** INFORMATION ****************************************************************
*   Routine Name : SIENA.BC.FXSPOT.HELPER
*           Type : SUBROUTINE
*         Params : PROCESS.NAME
*            Loc : SIENA.BP
** AUDIT **********************************************************************
*   Info Updated : 20210120 at 09.55.28 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE UTIL.BP F_UTIL.TRANSLATE.H
$INCLUDE UTIL.BP I_RATECSV.H
$INCLUDE UTIL.BP I_UTIL.H

* INITIALISE
   PROCESS.NAME = "SIENA.BC.FXSPOT.HELPER"
   CALL U_START(PROCESS.NAME)
* Setup some temp locations for input data


   ERROR.TEXT = ""; STOP.ON.ERROR = @TRUE ; VERBOSE = @TRUE
   RVDATA = ""
* OPEN FILES

   FN.SIENA.PROCESSED = "SIENA.PROCESSED" ; FV.SIENA.PROCESSED = ""
   CALL U_IO.OPENFILE(FN.SIENA.PROCESSED, FV.SIENA.PROCESSED, STOP.ON.ERROR, ERROR.TEXT)
   FN.SIENA.STAGING = "SIENA.STAGING" ; FV.SIENA.STAGING = ""
   CALL U_IO.OPENFILE(FN.SIENA.STAGING, FV.SIENA.STAGING, STOP.ON.ERROR, ERROR.TEXT)

   CALL U_HEADER("SIENA - FX SPOT - BARCHART.COM")

   URI.PREFIX = "" ; URI.SUFFIX = "" ; RVTYPE = "" ; RVFRIG = ""
   CALL U_IO.GET.PROPERTY("siena_ccyPair", "uriPrefix", URI.PREFIX)
   CALL U_IO.GET.PROPERTY("siena_ccyPair", "uriSPSuffix", URI.SUFFIX)
   CALL U_IO.GET.PROPERTY("siena_ccyPair", "RVType", RVTYPE)
   CALL U_IO.GET.PROPERTY("siena_ccyPair", "RVFrig", RVFRIG)
   CALL U_IO.GET.PROPERTY("siena_ccyPair", "precision", precisionValue)

   PRECISION precisionValue

   CCYPAIR.LIST = "" ; NO.PAIRS = -1
   CALL U_IO.GET.LIST("siena_ccyPair", "X", CCYPAIR.LIST, NO.PAIRS)

   CALL U_CRT.INFO("NO PAIRS", NO.PAIRS)
   OUTPUT.DATA = ""

   FOR THIS.PAIR = 1 TO NO.PAIRS
      CURRENCY.PAIR = CCYPAIR.LIST<THIS.PAIR>
      CALL U_CRT.INFO("FETCH", CURRENCY.PAIR)
      STMT.URI = URI.PREFIX:CURRENCY.PAIR:URI.SUFFIX
      *  CALL U_CRT.INFO("URI", STMT.URI)
      *CALL U_OS.CHROME.FETCH(STMT.URI, @TRUE, RESULT)
      CALL U_OS.WGET(STMT.URI, @TRUE, RESULT)
      IF RESULT<> "" THEN
         GOSUB PROCESS.HTML
      END ELSE
         CALL U_CRT.INFO("INFORMATION", "RESPONSE EMPTY!")
      END

   NEXT THIS.PAIR

   IF OUTPUT.DATA <> "" THEN
      ID.SIENA.STAGING = "RVMARKET":U_SEP_ALT:"BCSP"
      CALL U_CRT.INFO("DELETE", ID.SIENA.STAGING)
      CALL U_IO.DELETE(FV.SIENA.STAGING, ID.SIENA.STAGING, STOP.ON.ERROR, ERROR.TEXT)
      CALL U_CRT.INFO("STORE", ID.SIENA.STAGING)
      CALL U_IO.WRITE(FV.SIENA.STAGING, ID.SIENA.STAGING, OUTPUT.DATA, STOP.ON.ERROR, ERROR.TEXT)
   END

   CALL U_CRT.INFO("PROCESSING", "COMPLETE")
   CALL U_STOP(PROCESS.NAME)

   RETURN
*-------------------------------------
PROCESS.HTML:
* --------------------------
* WORKING ON CURRENCY PAIR
* --------------------------
*
* remove all @AM's we need this as one flat string
   ORIG.SEARCH.DATA = CHANGE(RESULT, CHAR(10), "")
   ORIG.SEARCH.DATA = CHANGE(ORIG.SEARCH.DATA, @AM, "")
   ORIG.SEARCH.DATA = TRIM(CHANGE(ORIG.SEARCH.DATA, CHAR(13), ""), " ")

   CALL U_CRT.INFO("INFORMATION", "DATA RECVD")
*   CALL U_CRT.RECORD(ORIG.SEARCH.DATA)

   *SEARCH.STRING = 'bidPrice&quot;:&quot;'s
   SEARCH.STRING = '"bidPrice":"'
   GOSUB SEARCH.FOR.PRICE
   BID.STRING = GET.STRING

   * SEARCH.STRING = 'askPrice&quot;:&quot;'
   SEARCH.STRING = '"askPrice":"'
   GOSUB SEARCH.FOR.PRICE
   ASK.STRING = GET.STRING

   IF NUM(BID.STRING) AND NUM(ASK.STRING) THEN
      * Frig the feed data - SIENA is multiplying by a factor of 1000
      BID.STRING=TRIMS((BID.STRING+0), "0", "T")
      ASK.STRING=TRIMS((ASK.STRING+0), "0", "T")
      IF BID.STRING <> "" AND ASK.STRING <> "" THEN
         GOSUB ADD.RVDATA
      END

   END


   RETURN
*-------------------------------------
ADD.RVDATA:
   OUT.CCYTN = 's':CURRENCY.PAIR"L#6":"spt"
   OUT.BUY = "D":LEFT(BID.STRING, 11)"R#11"
   OUT.SEL = "D":LEFT(ASK.STRING, 11)"R#11"
   ROW.DATA = OUT.CCYTN:OUT.BUY:OUT.SEL
   CALL U_CRT.INFO(CURRENCY.PAIR, ROW.DATA)
   OUTPUT.DATA<-1> = ROW.DATA

   * UPDATE BUY
   OBJECT.NAME = "FX":U_SEP_ID:"SP":U_SEP_ID:CURRENCY.PAIR:U_SEP_ID:"B"
   OBJECT.VALUE = BID.STRING+0
   CALL SIENA.CACHE.CLEAR(OBJECT.NAME)
   CALL SIENA.CACHE.UPDATE(OBJECT.NAME, "", OBJECT.VALUE)

   * UPDATE SELL
   OBJECT.NAME = "FX":U_SEP_ID:"SP":U_SEP_ID:CURRENCY.PAIR:U_SEP_ID:"O"
   OBJECT.VALUE = ASK.STRING+0
   CALL SIENA.CACHE.CLEAR(OBJECT.NAME)
   CALL SIENA.CACHE.UPDATE(OBJECT.NAME, "", OBJECT.VALUE)


   RETURN

*-------------------------------------
SEARCH.FOR.PRICE:
   SEARCH.DATA = CHANGE(ORIG.SEARCH.DATA, SEARCH.STRING, @AM)
   * CALL U_CRT.RECORD(SEARCH.DATA)
   END.SEARCH.STRING = '",'
   END.SEARCH.DATA = CHANGE(SEARCH.DATA<2>, END.SEARCH.STRING, @AM)
   * CALL U_CRT.RECORD(END.SEARCH.DATA)
   GET.STRING = END.SEARCH.DATA<1>
   CALL U_CRT.INFO("RESULT$STR", GET.STRING)
   RETURN
*-------------------------------------
END
