SUBROUTINE SIENA.GILTS.HELPER(PROCESS.NAME)
$INCLUDE UTIL.BP UTIL.H
$INCLUDE UTIL.BP UKTDATA.H
$INCLUDE UTIL.BP I_TRANSLATE.H
*** https://www.londonstockexchange.com/download/csv/tbLSEBondsDownloadCSV_en.csv
***
* INITIALISE
   CALL U_START(PROCESS.NAME)
* Setup some temp locations for input data

   ERROR.TEXT = ""; STOP.ON.ERROR = @TRUE ; VERBOSE = @TRUE
*  OPEN FILES

   FN.SIENA.STAGING = "SIENA.STAGING" ; FV.SIENA.STAGING = ""
   CALL U_OPENFILE(FN.SIENA.STAGING, FV.SIENA.STAGING, STOP.ON.ERROR, ERROR.TEXT)

   FN.SIENA.CONFIG = "SIENA.CONFIG" ; FV.SIENA.CONFIG = ""
   CALL U_OPENFILE(FN.SIENA.CONFIG, FV.SIENA.CONFIG, STOP.ON.ERROR, ERROR.TEXT)

   CALL U_HEADER("SIENA - Gilts")

   * CALL U_READ(FV.SIENA.CONFIG, "fetch.properties", R.SIENA.CONFIG, STOP.ON.ERROR, ERROR.TEXT)

   * CALL U_CRT.RECORD(R.SIENA.CONFIG)
****
   URI = "https://www.londonstockexchange.com/download/csv/tbLSEBondsDownloadCSV_en.csv"
   CSV.DATA = ""
   ID.SIENA.TEMP = ""
   CALL U_CURL(URI, @TRUE, R.SIENA.TEMP, ID.SIENA.TEMP)
   CALL U_CRT.ACTION("ID", ID.SIENA.TEMP)
   *CALL U_CRT.ACTION("BODY", R.SIENA.TEMP)

   * CALL U_CRT.RECORD(R.SIENA.TEMP)

   R.SIENA.TEMP = CONVERT(",", @VM, R.SIENA.TEMP)

   *CALL U_CRT.ACTION("POO","WEE")
   XML.REC = ""
   PRICE.REC = ""
   SIENA.ACTION = "IMPORT"
   SIENA.TABLE.NAME = "NegotiableInstrument"
   CALL SIENA.XML.HEADER(XML.REC, SIENA.ACTION, SIENA.TABLE.NAME)

   NO.ROWS = DCOUNT(R.SIENA.TEMP, @AM)

   CALL U_CRT.ACTION("ROWS", NO.ROWS)
   * header ROWS ARE 7 data 8 ONWARD

   FOR NO.ROW = 6 TO NO.ROWS
      * CALL U_CRT.ACTION(ROW.NO,R.SIENA.TEMP<ROW.NO,UKTR_LONG.NAME>)

      CALL U_CRT.ACTION("PROGRESS", NO.ROW)
      GOSUB DO.PROCESS.ROW
      IF MATURITY.DATE <> "" THEN
         GOSUB DO.CONTRACT.DEFINITION
         GOSUB DO.PRICE.INFO
      END
   NEXT NO.ROW

   CALL SIENA.XML.FOOTER(XML.REC)
   * CALL U_CRT.RECORD(XML.REC)
   CALL U_WRITE(FV.SIENA.STAGING, "SDI_NI.xml", XML.REC, STOP.ON.ERROR, ERROR.TEXT)
   CALL U_WRITE(FV.SIENA.STAGING, "RVNI", PRICE.REC, STOP.ON.ERROR, ERROR.TEXT)

   CALL U_STOP(PROCESS.NAME)

   RETURN

DO.PROCESS.ROW:

   LONG.NAME = R.SIENA.TEMP<NO.ROW, UKTR_LONG.NAME>
   ISIN = R.SIENA.TEMP<NO.ROW, UKTR_ISIN>
   TIDM = R.SIENA.TEMP<NO.ROW, UKTR_TIDM>
   SEDOL = R.SIENA.TEMP<NO.ROW, UKTR_SEDOL>
   ISSUE.DATE = R.SIENA.TEMP<NO.ROW, UKTR_ISSUE.DATE>
   MATURITY.DATE = R.SIENA.TEMP<NO.ROW, UKTR_MATURITY.DATE>
   COUPON.VALUE = R.SIENA.TEMP<NO.ROW, UKTR_COUPON.VALUE>
   COUPON.TYPE = R.SIENA.TEMP<NO.ROW, UKTR_COUPON.TYPE>
   SEGMENT = R.SIENA.TEMP<NO.ROW, UKTR_SEGMENT>
   SECTOR = R.SIENA.TEMP<NO.ROW, UKTR_SECTOR>
   CODE.CONVENTION.CALCULATE.ACCRUAL = R.SIENA.TEMP<NO.ROW, UKTR_CODE.CONVENTION.CALCULATE.ACCRUAL>
   MINIMUM.DENOMINATION = R.SIENA.TEMP<NO.ROW, UKTR_MINIMUM.DENOMINATION>
   DENOMINATION.CURRENCY = R.SIENA.TEMP<NO.ROW, UKTR_DENOMINATION.CURRENCY>
   TRADING.CURRENCY = R.SIENA.TEMP<NO.ROW, UKTR_TRADING.CURRENCY>
   TYPE = R.SIENA.TEMP<NO.ROW, UKTR_TYPE>
   STATUS = "Incomplete"
   IF TYPE = "Gilt" THEN
      STATUS = "Active"
   END
   FLAT.YIELD = R.SIENA.TEMP<NO.ROW, UKTR_FLAT.YIELD>
   PAYMENT.COUPON.DATE = R.SIENA.TEMP<NO.ROW, UKTR_PAYMENT.COUPON.DATE>
   PERIOD.OF.COUPON = R.SIENA.TEMP<NO.ROW, UKTR_PERIOD.OF.COUPON>
   EX.COUPON.DATE = R.SIENA.TEMP<NO.ROW, UKTR_EX.COUPON.DATE>
   DATE.OF.INDEX.INFLATION = R.SIENA.TEMP<NO.ROW, UKTR_DATE.OF.INDEX.INFLATION>
   UNIT.OF.QUOTATION = R.SIENA.TEMP<NO.ROW, UKTR_UNIT.OF.QUOTATION>

   CALL U_TRANSLATE("COUPON.TYPE", R.SIENA.TEMP<NO.ROW, UKTR_COUPON.TYPE>, U_TRNS.UKTDATA, COUPON.TYPE)
   CALL U_TRANSLATE("SEGMENT", R.SIENA.TEMP<NO.ROW, UKTR_SEGMENT>, U_TRNS.UKTDATA, SEGMENT)
   CALL U_TRANSLATE("SECTOR", R.SIENA.TEMP<NO.ROW, UKTR_SECTOR>, U_TRNS.UKTDATA, SECTOR)
   CALL U_TRANSLATE("ACCRUAL", R.SIENA.TEMP<NO.ROW, UKTR_CODE.CONVENTION.CALCULATE.ACCRUAL>, U_TRNS.UKTDATA, CODE.CONVENTION.CALCULATE.ACCRUAL)
   CALL U_TRANSLATE("TYPE", R.SIENA.TEMP<NO.ROW, UKTR_TYPE>, U_TRNS.UKTDATA, TYPE)
   CALL U_TRANSLATE("PERIOD", R.SIENA.TEMP<NO.ROW, UKTR_PERIOD.OF.COUPON>, U_TRNS.UKTDATA, PERIOD.OF.COUPON)
   THIS.FIRM = ""
   THIS.CENTER = ""
   CALL U_TRANSLATE("FIRM", R.SIENA.TEMP<NO.ROW, UKTR_SEGMENT>, U_TRNS.UKTDATA, THIS.FIRM)
   CALL U_TRANSLATE("CENTER", R.SIENA.TEMP<NO.ROW, UKTR_SEGMENT>, U_TRNS.UKTDATA, THIS.CENTER)
   THIS.PARENTDEAL=""
   CALL U_TRANSLATE("PARENTDEAL", R.SIENA.TEMP<NO.ROW, UKTR_TYPE>, U_TRNS.UKTDATA, THIS.PARENTDEAL)
   ISSUE.DATE = CONVERT(" ", "", OCONV(ICONV(ISSUE.DATE, "D"), "DYMD"))
   MATURITY.DATE = CONVERT(" ", "", OCONV(ICONV(MATURITY.DATE, "D"), "DYMD"))


  CALL SIENA.NI.NAME(LONG.NAME,COUPON.VALUE,MATURITY.DATE)

   * CRT LONG.NAME
   * CRT COUPON.TYPE
   * CRT SEGMENT
   * CRT SECTOR
   * CRT CODE.CONVENTION.CALCULATE.ACCRUAL
   * CRT TYPE


   RETURN



DO.CONTRACT.DEFINITION:
   XML.REC<-1>="<RECORD>"

   CALL SIENA.XML.FIELD(XML.REC, "KEYFIELD", "name", LONG.NAME)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "status", STATUS)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "type", TYPE)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "currencyCode", TRADING.CURRENCY)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "issuer.firm", THIS.FIRM)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "issuer.centre", THIS.CENTER)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "acceptor.firm", "")
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "acceptor.centre", "")
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "issueDate", ISSUE.DATE)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "maturityDate", MATURITY.DATE)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "rateType", COUPON.TYPE)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "couponRate", COUPON.VALUE)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "interestBasis", CODE.CONVENTION.CALCULATE.ACCRUAL)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "interestFrequency", PERIOD.OF.COUPON)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "clearingSystem", "")
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "parentDealType", THIS.PARENTDEAL)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "securityID", ISIN)
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "encumbered", "")
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "marketable", "")
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "hypothecated", "")
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "repoFactor", "100.00")
   CALL SIENA.XML.FIELD(XML.REC, "FIELD", "lotSize", MINIMUM.DENOMINATION)

   XML.REC<-1>="</RECORD>"
   RETURN

DO.PRICE.INFO:

   THIS.EXAMPLE = "https://www.londonstockexchange.com/exchange/prices-and-markets/retail-bonds/company-summary/XS0178489844ZZGBPUKCP.html?lang=en"
   THIS.PREFIX = "https://www.londonstockexchange.com/exchange/prices-and-markets/retail-bonds/company-summary/"
   THIS.SUFFIX = ".html"


   THIS.FETCH.ID= ISIN:SECTOR:TRADING.CURRENCY:SEGMENT

   IF LEN(THIS.FETCH.ID)>4 THEN

      *    CALL U_CRT.ACTION("ISIN", ISIN)
      *    CALL U_CRT.ACTION("SECTOR", SECTOR)
      *    CALL U_CRT.ACTION("TRADCCY", TRADING.CURRENCY)
      *    CALL U_CRT.ACTION("SEGMENT", SEGMENT)

      THIS.URI=THIS.PREFIX:THIS.FETCH.ID:THIS.SUFFIX
      *    CALL U_CRT.ACTION("FETCH", THIS.URI )
      RETURN.DATA = '' ; RETURN.ID = ''
      CALL U_CURL(THIS.URI, @TRUE, RETURN.DATA, RETURN.ID)

* Search return data for "<td class="name">Price&nbsp;(GBP)</td>"
* Price is in the next cell
*  <!-- START SECURITY BOOK PRICE DATA Sez 1 -->
*<div class="commonTable table-responsive">
*<table width="100%" cellspacing="0" cellpadding="0" summary="Price data">
*<tbody>
*<tr class="even">
*<td class="name">Price&nbsp;(GBP)</td>
*<td>111.07</td>
*<td class="name">Var % (+/-)</td>
*<td>

      SEARCH.STRING = '<td class="name">Price&nbsp;(':TRADING.CURRENCY:')</td>'

      *CALL U_CRT.ACTION("SEARCH", SEARCH.STRING)

      SEARCH.DATA = CHANGE(RETURN.DATA, CHAR(10), "")
      SEARCH.DATA = CHANGE(SEARCH.DATA, @AM, "")
      SEARCH.DATA = TRIM(CHANGE(SEARCH.DATA, CHAR(13), ""), " ")

      SEARCH.DATA = CHANGE(SEARCH.DATA, SEARCH.STRING, @AM)
      SEARCH.DATA<2> = CHANGE(SEARCH.DATA<2>, "<td>", @VM)
      SEARCH.DATA<2> = CHANGE(SEARCH.DATA<2>, "</td>", @VM)
      * CALL U_CRT.ACTION("EXTRACTPOS", SEARCH.DATA<2, 1>)
      * CALL U_CRT.ACTION("EXTRACTPOS", SEARCH.DATA<2, 2>)
      * CALL U_CRT.ACTION("EXTRACTPOS", SEARCH.DATA<2, 3>)

      GILT.PRICE = SEARCH.DATA<2, 2>+0
      GILT.PRICE = GILT.PRICE"L#11"
      * CALL U_CRT.ACTION("PRICE", GILT.PRICE)

      PRICE.REC<-1> = LONG.NAME"L#30":"+":GILT.PRICE:"+":GILT.PRICE

   END

   RETURN

END
