SUBROUTINE SIENA.GEN.TXN.DATA(PROCESS.TYPE)
** INFORMATION ****************************************************************
*   Routine Name : SIENA.GEN.TXN.DATA
*           Type : SUBROUTINE
*         Params : PROCESS.TYPE
*            Loc : SIENA.BP
** AUDIT **********************************************************************
*   Info Updated : 20210120 at 09.22.46 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INSERT UTIL.BP I_UTIL.H

   PROCESS.NAME = "SIENA.GEN.TXN.DATA-":UPCASE(PROCESS.TYPE)
   CALL U_INITIALISE(PROCESS.NAME)
   CALL U_HEADER("GENERATING>>>":UPCASE(PROCESS.TYPE))

*** OPENFILES STARTS
   FN.SIENA.TXN.TEMPLATES = "SIENA.TXN.TEMPLATES" ; FV.SIENA.TXN.TEMPLATES = ""
   CALL U_IO.OPENFILE(FN.SIENA.TXN.TEMPLATES, FV.SIENA.TXN.TEMPLATES, @FALSE, "")

   CALL U_IO.GET.PROPERTY("siena_demo", "demo_data_":DOWNCASE(PROCESS.TYPE), DATA.MAP.FILE)
   IF DATA.MAP.FILE = "" THEN
      CALL U_CRT.INFO("INFO", "No Data Mapping found, SKIPPING")
      CALL U_CRT.INFO("INFO", "demo_data_":DOWNCASE(PROCESS.TYPE):".list not found in UTIL.CONFIG/siena_demo.properties")

      * NAUGHTY BUT WORKS
      RETURN
   END
* GET FIELD MAP
   DATA.MAP = "" ; NO.MAPPINGS = 0
   CALL U_IO.GET.LIST(DATA.MAP.FILE, "MAP", DATA.MAP, NO.MAPPINGS)

* GET DATA TO LOAD
   DATA.ARR = "" ; NO.TRANSACTIONS = 0
   CALL U_IO.GET.LIST(DATA.MAP.FILE, "DATA", DATA.ARR, NO.TRANSACTIONS)

   FP.DESTINATION = "" ; FV.DESTINATION = ""
   CALL U_IO.GET.PROPERTY("siena_demo", "demo_dest_":LOWER(PROCESS.TYPE), FP.DESTINATION)
*   CALL U_CRT.INFO("DELIVER", FP.DESTINATION)
   CALL U_IO.OPENFILEPATH(FP.DESTINATION, FV.DESTINATION, @TRUE, "")
*** OPENFILES ENDS

*** GET TEMPLATES
   CALL U_IO.GET.PROPERTY("siena_demo", "demo_tmpl_":LOWER(PROCESS.TYPE), ID.SIENA.TXN.TEMPLATES)
*   CALL U_CRT.INFO("TEMPLATE", ID.SIENA.TXN.TEMPLATES)

   CALL U_IO.READ(FV.SIENA.TXN.TEMPLATES, ID.SIENA.TXN.TEMPLATES, R.SIENA.TXN.TEMPLATES, @FALSE, "")
*** GET TEMPLATES END

*   CALL U_CRT.INFO("MAPS", NO.MAPPINGS)
   CALL U_CRT.INFO("ROWS", NO.TRANSACTIONS)

*   CALL U_CRT.RECORD(DATA.MAP)
*   CALL U_CRT.RECORD(DATA.ARR)

   FOR THIS.DATA.ROW = 1 TO NO.TRANSACTIONS
      SOURCE.DATA = CHANGE(DATA.ARR<THIS.DATA.ROW>, U_PROP_SEP_ALT, @AM)
      *  CALL U_CRT.INFO("ITEM", THIS.DATA.ROW:"/":NO.TRANSACTIONS)
      *    CALL U_CRT.RECORD(SOURCE.DATA)
      GOSUB PROCESS.ROW
   NEXT THIS.DATA.ROW

   RETURN

PROCESS.ROW:

   R.NEW.TXN = R.SIENA.TXN.TEMPLATES

   FOR THIS.MAPPING = 1 TO NO.MAPPINGS
      FIND.TAG = DATA.MAP<THIS.MAPPING>
      SUBS.DATA = SOURCE.DATA<THIS.MAPPING>
      IF SUBS.DATA[1, 1]="!" THEN
         FETCH.OBJECT=TRIM(SUBS.DATA[2, 99])
         CALL SIENA.CACHE.FETCH(FETCH.OBJECT, SUBS.DATA)
      END
      CALL U_XML.SUBSTITUTE(FIND.TAG, SUBS.DATA, R.NEW.TXN)
   NEXT THIS.MAPPING
*   CALL U_CRT.INFO("START", "WILDCARD MAPPING")
* YEAR, MONTH, DAY, HOURS, MINS, SECS
   CALL U_XML.SUBSTITUTE("YYYY", @YEAR4, R.NEW.TXN)
   CALL U_XML.SUBSTITUTE("MM", @MONTH, R.NEW.TXN)
   CALL U_XML.SUBSTITUTE("DD", @DAY, R.NEW.TXN)
   CALL U_XML.SUBSTITUTE("HH", OCONV(TIME(), "MTS.")[1, 2], R.NEW.TXN)
   CALL U_XML.SUBSTITUTE("mm", OCONV(TIME(), "MTS.")[4, 2], R.NEW.TXN)
   CALL U_XML.SUBSTITUTE("SS", OCONV(TIME(), "MTS.")[7, 2], R.NEW.TXN)
*     <!-- Result. 1 means OK -->
*   CALL U_XML.SUBSTITUTE("RESULTCODE", "1", R.NEW.TXN)

* FIRST WORKING DAY OF YEAR
   NEW.DATE = @YEAR4-1:"12":"31"
   NEW.DATE = ICONV(NEW.DATE+1, "DX")
   CALL U_GET.NEXT.WORKING.DAY(NEW.DATE, INT.DATE)
   GOSUB FMT.SIENA.DATA
   CALL U_XML.SUBSTITUTE("FIRST.WD.OF.YEAR", SIENA.DATE, R.NEW.TXN)

   REPLACE.ITEM = "TODAY"
   INT.DATE = DATE() ; GOSUB FMT.SIENA.DATA
   CALL U_XML.SUBSTITUTE(REPLACE.ITEM, SIENA.DATE, R.NEW.TXN)

   REPLACE.ITEM = "VALUE.DATE.SPOT"
   VDATE = DATE()+2 ; NEXT.WORKING.DAY=""
   CALL U_GET.NEXT.WORKING.DAY(VDATE, INT.DATE)
   GOSUB FMT.SIENA.DATA
   CALL U_XML.SUBSTITUTE(REPLACE.ITEM, SIENA.DATE, R.NEW.TXN)

   REPLACE.ITEM = "NOW"
   REPLACE.VALUE = OCONV(TIME(), "MTS")
   CALL U_XML.SUBSTITUTE(REPLACE.ITEM, REPLACE.VALUE, R.NEW.TXN)

   ID.RESPONSE = ""
   CALL U_GET.UUID(ID.RESPONSE)
   CALL U_XML.SUBSTITUTE("MSG.ID", ID.RESPONSE, R.NEW.TXN)

   CALL U_CRT.INFO("PROCESSED", THIS.DATA.ROW:"/":NO.TRANSACTIONS:" ":DQUOTE(FP.DESTINATION:"/":ID.RESPONSE))
*   CALL U_CRT.RECORD(R.NEW.TXN)
   CALL U_IO.WRITE(FV.DESTINATION, ID.RESPONSE, R.NEW.TXN, @FALSE, "")

   RETURN

FMT.SIENA.DATA:
   SIENA.DATE = ""
   SIENA.DATE = CHANGE(OCONV(INT.DATE, "DYMD[4,2,2]"), " ", "-")
   INT.DATE = ""
   RETURN


END
