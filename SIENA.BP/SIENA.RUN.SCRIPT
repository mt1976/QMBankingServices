PROGRAM SIENA.RUN.SCRIPT
** INFORMATION ****************************************************************
* Routine Name   : SIENA.RUN.SCRIPT
*         Type   : PROGRAM
*         Params :
*         Loc    : SIENA.BP
** AUDIT **********************************************************************
* Info Updated   : 20200507 at 08.02.01 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE UTIL.BP I_UTIL.H
   CALL U_INITIALISE("SIENA.RUN.SCRIPT")
   PNM = "" ; STOP.ON.ERROR = @TRUE ; ERROR.TEXT = ""
   CALL U_START(PNM)
   CALL U_HEADER("SIENA - RUN SCRIPT")

   FN.SIENA.SCRIPTS = "SIENA.SCRIPTS" ; FV.SIENA.SCRIPTS = ""
   CALL U_IO.OPENFILE(FN.SIENA.SCRIPTS, FV.SIENA.SCRIPTS, STOP.ON.ERROR, ERROR.TEXT)

   CMD.STRING = CONVERT(SPACE(1), @AM, @SENTENCE)
   IF CMD.STRING<2> = "" THEN
      CALL U_CRT.INFO("ERROR", "Parameters [ALL, or specific]")
      CALL U_STOP(PNM)
      STOP
   END

   * remove the program name from the stack of cmds
   DEL CMD.STRING<1>
   * loop through the cmd strings
   NO.CMDS = DCOUNT(CMD.STRING, @AM)

*   CALL U_CRT.INFO("NO ITEMS", NO.CMDS)
   FOR THIS.CMD = 1 TO NO.CMDS
      SCRIPT.NAME = CMD.STRING<THIS.CMD>
*      CALL U_CRT.INFO("PROCESS", SCRIPT.NAME)
      STMT = "SELECT ":FN.SIENA.SCRIPTS:" LIKE ...":SCRIPT.NAME:"..."
      IF SCRIPT.NAME = "ALL" THEN
         STMT = "SELECT ":FN.SIENA.SCRIPTS
      END
      GOSUB PROCESS.IT
   NEXT THIS.CMD

   CALL U_STOP(PNM)

   STOP

PROCESS.IT:
*CALL U_CRT.INFO('STMT',STMT)

   CALL U_OS.EXECUTE(STMT, @TRUE)

   SCRIPT.LIST = ""
   LOOP
      READNEXT ID.SCRIPT ELSE EXIT
      SCRIPT.LIST<-1> = ID.SCRIPT
   REPEAT

   NO.SCRIPTS = DCOUNT(SCRIPT.LIST, @AM)

   FOR THIS.SCRIPT = 1 TO NO.SCRIPTS

      CALL SIENA.SCRIPT.HELPER(SCRIPT.LIST<THIS.SCRIPT>)

   NEXT THIS.SCRIPT
   RETURN



END
