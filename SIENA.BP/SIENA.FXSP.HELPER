SUBROUTINE SIENA.FXSP.HELPER(PROCESS.NAME)
** INFORMATION ****************************************************************
*   Routine Name : SIENA.FXSP.HELPER
*           Type : SUBROUTINE
*         Params : PROCESS.NAME
*            Loc : SIENA.BP
** AUDIT **********************************************************************
*   Info Updated : 20210214 at 22.36.10 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE UTIL.BP F_UTIL.TRANSLATE.H
$INCLUDE UTIL.BP I_RATECSV.H
$INCLUDE UTIL.BP I_UTIL.H
* INITIALISE
   PROCESS.NAME="SIENA.FXSP.HELPER"
   CALL U_START(PROCESS.NAME)
* Setup some temp locations for input data


   ERROR.TEXT = ""; STOP.ON.ERROR = @TRUE ; VERBOSE = @TRUE
* OPEN FILES
   FN.SIENA.IN = "SIENA.IN" ; FV.SIENA.IN = ""
   CALL U_IO.OPENFILE(FN.SIENA.IN, FV.SIENA.IN, STOP.ON.ERROR, ERROR.TEXT)
   FN.SIENA.PROCESSED = "SIENA.PROCESSED" ; FV.SIENA.PROCESSED = ""
   CALL U_IO.OPENFILE(FN.SIENA.PROCESSED, FV.SIENA.PROCESSED, STOP.ON.ERROR, ERROR.TEXT)
   FN.SIENA.STAGING = "SIENA.STAGING" ; FV.SIENA.STAGING = ""
   CALL U_IO.OPENFILE(FN.SIENA.STAGING, FV.SIENA.STAGING, STOP.ON.ERROR, ERROR.TEXT)

   CALL U_HEADER("SIENA - FX SPOT")

   STMT = "SELECT ":FN.SIENA.IN:" WITH @ID LIKE fx_SP_rates..."
   CALL U_OS.EXECUTE(STMT, VERBOSE)
   OUTPUT.DATA = ""  ;* INIT THE RATES FILE
   LOOP
      READNEXT ID.SIENA.IN ELSE EXIT

      CALL U_CRT.INFO("PROCESSING", ID.SIENA.IN)
      R.SIENA.IN = ""
      CALL U_IO.READ(FV.SIENA.IN, ID.SIENA.IN, R.SIENA.IN, STOP.ON.ERROR, ERROR.TEXT)
*CALL U_CRT.INFO("RECORD",R.SIENA.IN)

      NO.ROWS = DCOUNT(R.SIENA.IN, @FM)
*CALL U_CRT.INFO("ROWS",NO.ROWS)

      INPUT.DATA = CONVERT(",", @VM, R.SIENA.IN)

      FOR THIS.ROW = 1 TO NO.ROWS
         IF INPUT.DATA<THIS.ROW, 1> <> "" THEN

*CALL U_CRT.INFO("CCY1",INPUT.DATA<THIS.ROW,RATE.CCY1>)
*CALL U_CRT.INFO("CCY2",INPUT.DATA<THIS.ROW,RATE.CCY2>)
            CCYPAIR = INPUT.DATA<THIS.ROW, RATE.CCY1>:INPUT.DATA<THIS.ROW, RATE.CCY2>
            TMP.TENOR = INPUT.DATA<THIS.ROW, RATE.TENOR>
            OUT.TENOR = ""
            CALL U_TRANSLATE("TENOR", TMP.TENOR, U_TRNS.RVSERVER, OUT.TENOR)
            CALL U_TRANSLATE("RATETYPE", INPUT.DATA<THIS.ROW, RATE.TYPE>, U_TRNS.RVSERVER, OUT.TYPE)
            OUT.CCYTN = OUT.TYPE:CCYPAIR"L#6":OUT.TENOR

* CALL U_CRT.INFO("TCCT",OUT.CCYTN)
            OUT.BUY = "D":INPUT.DATA<THIS.ROW, RATE.BUY> "L#11"
            OUT.SEL = "D":INPUT.DATA<THIS.ROW, RATE.SELL> "L#11"
* CALL U_CRT.INFO("BUY",OUT.BUY)
* CALL U_CRT.INFO("SELL",OUT.SEL)
            ROW.DATA = OUT.CCYTN:OUT.BUY:OUT.SEL
* CALL U_CRT.INFO("OUTPUT",ROW.DATA)
            OUTPUT.DATA<-1> = ROW.DATA

            * UPDATE BUY
            OBJECT.NAME = "RATE":U_SEP:"FX":U_SEP:"B":U_SEP:"SP":CCYPAIR
            OBJECT.VALUE = OUT.BUY
            CALL SIENA.CACHE.CLEAR(OBJECT.NAME)
            CALL SIENA.CACHE.UPDATE(OBJECT.NAME, "", OBJECT.VALUE)

            * UPDATE SELL
            OBJECT.NAME = "RATE":U_SEP:"FX":U_SEP:"O":U_SEP:"SP":CCYPAIR
            OBJECT.VALUE = OUT.SEL
            CALL SIENA.CACHE.CLEAR(OBJECT.NAME)
            CALL SIENA.CACHE.UPDATE(OBJECT.NAME, "", OBJECT.VALUE)

         END
* INPUT.DATA = CONVERT("!", @FM, ID.RESTORE)



      NEXT THIS.ROW
      ID.SIENA.PROCESSED = ""
      CALL U_GET.UUID(ID.SIENA.PROCESSED)
*CALL U_CRT.INFO("STORE",ID.SIENA.PROCESSED)
      CALL U_IO.WRITE(FV.SIENA.PROCESSED, ID.SIENA.PROCESSED, R.SIENA.IN, STOP.ON.ERROR, ERROR.TEXT)
      CALL U_IO.DELETE(FV.SIENA.IN, ID.SIENA.IN, STOP.ON.ERROR, ERROR.TEXT)
   REPEAT

   IF OUTPUT.DATA <> "" THEN
      ID.SIENA.STAGING = "RVMARKET":U_SEP_ALT:"STDSP"
      CALL U_CRT.INFO("DELETE", ID.SIENA.STAGING)
      CALL U_IO.DELETE(FV.SIENA.STAGING, ID.SIENA.STAGING, STOP.ON.ERROR, ERROR.TEXT)
      CALL U_CRT.INFO("STORE", ID.SIENA.STAGING)
      CALL U_IO.WRITE(FV.SIENA.STAGING, ID.SIENA.STAGING, OUTPUT.DATA, STOP.ON.ERROR, ERROR.TEXT)
   END

   CALL U_CRT.INFO("PROCESSING", "COMPLETE")
   CALL U_STOP(PROCESS.NAME)
   RETURN
END
