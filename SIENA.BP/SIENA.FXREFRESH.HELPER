SUBROUTINE SIENA.FXREFRESH.HELPER(PROCESS.NAME)
** INFORMATION ****************************************************************
*   Routine Name : SIENA.FXREFRESH.HELPER
*           Type : SUBROUTINE
*         Params : PROCESS.NAME
*            Loc : SIENA.BP
** AUDIT **********************************************************************
*   Info Updated : 20200922 at 21.58.17 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE UTIL.BP I_UTIL.H
$INCLUDE UTIL.BP F_UTIL.LOG.EVENT.H
* INITIALISE
   PROCESS.NAME = "SIENA.FXREFRESH.HELPER"
   CALL U_START(PROCESS.NAME)
* Setup some temp locations for input data
   FN.SIENA.TEMP = "SIENA.TEMP" ; FV.SIENA.TEMP = "" ; STOP.ON.ERROR = @TRUE ; ERROR.TEXT = ""
   CALL U_IO.OPENFILE(FN.SIENA.TEMP, FV.SIENA.TEMP, STOP.ON.ERROR, ERROR.TEXT)
   ID.UTIL.CONFIG = ""

   ERROR.TEXT = ""; STOP.ON.ERROR = @TRUE ; VERBOSE = @TRUE
* OPEN FILES

   CALL U_HEADER("eurobase - Update CCY's to request")

   SOURCES.LIST = "" ; NO.SOURCES = -1
   CALL U_IO.GET.LIST("siena_ccyPairSources", "DBSOURCE", SOURCES.LIST, NO.SOURCES)

   FOR I = 1 TO NO.SOURCES

      THIS.SOURCE = SOURCES.LIST<I>
      THIS.SOURCE = CONVERT(U_PROP_SEP_ALT, @AM, THIS.SOURCE)

      SQL.FIELDS = "CodeMajorCurrencyIsoCode,CodeMinorCurrencyIsoCode"
*     SQL.TABLE    = "dbo.CurrencyPair"
*     SQL.TABLE    = "dbo.vwRateSourceCurrencyPair"
      SQL.TABLE = THIS.SOURCE<4>

*     SQL.WHERE    = "Active=1 and SpotRerouteDataRerouteCurrency=''"
      SQL.WHERE = CONVERT(U_PREF_SQL_EQ, "=", THIS.SOURCE<5>)

      RES.COLUMNS = ""
      RES.DATA = ""
      RES.COUNT = ""
      SQL.KNOWN.AS = THIS.SOURCE<1>
      SQL.ADDRESS = THIS.SOURCE<2>
      SQL.DATABASE = THIS.SOURCE<3>
      SQL.FIELDS = THIS.SOURCE<6>

      CALL U_CRT.INFO("SOURCE", THIS.SOURCE)

      GOSUB UPDATE.LIST
   NEXT I

   CALL U_STOP(PROCESS.NAME)

   RETURN

UPDATE.LIST:
   CALL U_MSSQL.SELECT(SQL.FIELDS, SQL.TABLE, SQL.ADDRESS, SQL.DATABASE, SQL.WHERE, VERBOSE, ID.RESULT, RES.COLUMNS, RES.DATA, RES.COUNT)
   CALL U_CRT.INFO("RESULT", ID.RESULT)
   CALL U_CRT.RECORD(RES.COLUMNS)
   CALL U_CRT.RECORD(RES.DATA)
   CALL U_CRT.INFO("NO RECORDS", RES.COUNT)

   ID.UTIL.CONFIG = "tmp_":SQL.KNOWN.AS:"_siena_ccyPair.list"

   R.NEW.LIST = ""
   *R.NEW.LIST<-1> = "#=----------------------------------------------------------------------------="
   *R.NEW.LIST<-1> = "#=AUTO GENERATED - FX SPOT & FORWARD CURVES TO FETCH="
   *R.NEW.LIST<-1> = "#=GENERATED at ":OCONV(TIME(),"MTS."):" on ":OCONV(DATE(),"D4/"):"="
   *R.NEW.LIST<-1> = "#=Temp file is ":ID.UTIL.CONFIG:" in UTIL.CONFIG="
   *R.NEW.LIST<-1> = "#=----------------------------------------------------------------------------="

   FOR THIS.PAIR.POS = 1 TO RES.COUNT
      CCY.PAIR = RES.DATA<THIS.PAIR.POS, 1>:RES.DATA<THIS.PAIR.POS, 2>
      CALL U_CRT.INFO("PAIR", CCY.PAIR)
      R.NEW.LIST<-1> = "X=":CCY.PAIR:"="
   NEXT THIS.PAIR.POS

   IF R.NEW.LIST <> "" THEN
      CALL U_IO.WRITE(FV.SIENA.TEMP, ID.UTIL.CONFIG, R.NEW.LIST, STOP.ON.ERROR, ERROR.TEXT)
   END

   R.UTIL.LOG.EVENT = ""
   R.UTIL.LOG.EVENT<U_LOG_EVENT> = 'REGENERATE'
   R.UTIL.LOG.EVENT<U_LOG_MSG> = 'UTIL.CONFIG UPDATE SUCCESS'
   IF R.NEW.LIST = "" THEN
      R.UTIL.LOG.EVENT<U_LOG_MSG> = 'UTIL.CONFIG UPDATE NO DATA'
   END
   R.UTIL.LOG.EVENT<U_LOG_ID> = ID.UTIL.CONFIG

   CALL U_CRT.RECORD(R.NEW.LIST)
   CALL U_LOG.EVENT("", R.UTIL.LOG.EVENT)

   RETURN

END
