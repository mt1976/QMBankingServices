SUBROUTINE SIENA.SONIA.HELPER(PROCESS.NAME)
** INFORMATION ****************************************************************
*   Routine Name : SIENA.BOESONIA.HELPER
*           Type : SUBROUTINE
*         Params : PROCESS.NAME
*            Loc : SIENA.BP
** AUDIT **********************************************************************
*   Info Updated : 20210128 at 12.20.50 in DEV by root
*                : on mercury.local (Mac)
*******************************************************************************

$INCLUDE UTIL.BP I_UTIL.H
$INCLUDE UTIL.BP F_UTIL.LOG.EVENT.H
$INCLUDE UTIL.BP F_UTIL.TRANSLATE.H
* INITIALISE
   PROCESS.NAME = "SIENA.SONIA.HELPER"
   CALL U_START(PROCESS.NAME)
* Setup some temp locations for input data

   ERROR.TEXT = ""; STOP.ON.ERROR = @TRUE ; VERBOSE = @TRUE
* OPEN FILES

   FN.SIENA.STAGING = "SIENA.STAGING" ; FV.SIENA.STAGING = ""
   CALL U_IO.OPENFILE(FN.SIENA.STAGING, FV.SIENA.STAGING, STOP.ON.ERROR, ERROR.TEXT)
* INITIALISE
   BOE.URL=""

   ID.UTIL.CONFIG="boe.url"
   CALL U_IO.READ(FV.UTIL.CONFIG, ID.UTIL.CONFIG, BOE.URL, STOP.ON.ERROR, ERROR.TEXT)


   TO.DATE = DATE()
   FROM.DATE = TO.DATE-2

   REPLACE.STRING = "<<FDD>>"
   REPLACE.VALUE = OCONV(FROM.DATE, "D")[1, 2]
   GOSUB REPLACE.VALUES

   REPLACE.STRING = "<<FDM>>"
   REPLACE.VALUE = OCONV(FROM.DATE, "DMAL")[1, 3]
   GOSUB REPLACE.VALUES

   REPLACE.STRING = "<<FDY>>"
   REPLACE.VALUE = OCONV(FROM.DATE, "D")[4]
   GOSUB REPLACE.VALUES


   REPLACE.STRING = "<<TDD>>"
   REPLACE.VALUE = OCONV(TO.DATE, "D")[1, 2]
   GOSUB REPLACE.VALUES

   REPLACE.STRING = "<<TDM>>"
   REPLACE.VALUE = OCONV(TO.DATE, "DMAL")[1, 3]
   GOSUB REPLACE.VALUES

   REPLACE.STRING = "<<TDY>>"
   REPLACE.VALUE = OCONV(TO.DATE, "D")[4]
   GOSUB REPLACE.VALUES

   CALL U_HEADER("Get SONIA RFR Rates")


   CALL U_CRT.INFO("URL", BOE.URL)
   CALL U_CRT.INFO("FROM", OCONV(FROM.DATE, "DX"))
   CALL U_CRT.INFO("TO", OCONV(TO.DATE, "DX"))
   RESULT=""
   * CALL U_OS.CHROME.FETCH(BOE.URL, @TRUE, RESULT)
   CALL U_OS.CURL(BOE.URL, @TRUE, RESULT, ID.RESULT)

   * CALL U_CRT.RECORD(RESULT)

   SONIA = ""
   GOSUB PROCESS.RESULT
   CALL U_CRT.INFO("SONIA", SONIA)

   CALL U_STOP(PROCESS.NAME)

   RETURN

REPLACE.VALUES:
*CALL U_CRT.INFO("REPLACE.STRING",REPLACE.STRING)
*CALL U_CRT.INFO("REPLACE.VALUE",REPLACE.VALUE)
   BOE.URL = CHANGE(BOE.URL, REPLACE.STRING, REPLACE.VALUE)
*CALL U_CRT.INFO("REPLACED.URL",BOE.URL)

   RETURN

PROCESS.RESULT:

   WRK.RESULT = RESULT

   WRK.RESULT = CHANGE(WRK.RESULT, @AM, "")
   WRK.RESULT = CHANGE(WRK.RESULT, CHAR(9), "")
   WRK.RESULT = CHANGE(WRK.RESULT, CHAR(10), "")
   WRK.RESULT = CHANGE(WRK.RESULT, CHAR(13), "")
   WRK.RESULT = CHANGE(WRK.RESULT, '<td width="75" align="right" nowrap>', @AM)<2>
   WRK.RESULT = CHANGE(WRK.RESULT, '</td>', @AM)
   WRK.RESULT = CHANGE(WRK.RESULT, '</tr>', "")
   WRK.RESULT = CHANGE(WRK.RESULT, '<td align="right">', "")

   * CALL U_CRT.RECORD(WRK.RESULT)
   SONIA=WRK.RESULT<2>

   OUTPUT.DATA=""
   TMP.TENOR = "BOESONIA"
   OUT.TENOR = ""
   EXT.TENOR = ""
   CCY = ""
   CALL U_TRANSLATE("TENOR", TMP.TENOR, U_TRNS.RVSERVER, OUT.TENOR)
   CALL U_TRANSLATE("TENOR", TMP.TENOR, U_TRNS.SIENA_BC, EXT.TENOR)
   CALL U_TRANSLATE("RATETYPE", TMP.TENOR, U_TRNS.RVSERVER, OUT.TYPE)
   CALL U_TRANSLATE("CCY", TMP.TENOR, U_TRNS.RVSERVER, CCY)
   OUT.CCYTN = OUT.TYPE:CCY"L#6":OUT.TENOR
   OUT.BUY = CONVERT('"', '', SONIA) "L#11"
   OUT.SEL = CONVERT('"', '', SONIA) "L#11"

   ROW.DATA = OUT.CCYTN:"D":OUT.BUY:"D":OUT.SEL
   IF NUM(OUT.BUY) AND NUM(OUT.SEL) THEN
      OUTPUT.DATA<-1> = ROW.DATA
      * UPDATE BUY
      OBJECT.NAME = "SONIA":U_SEP_ID:EXT.TENOR:U_SEP_ID:TRIM(CCY)
      OBJECT.VALUE = OUT.BUY+0
      CALL SIENA.CACHE.CLEAR(OBJECT.NAME)
      CALL SIENA.CACHE.UPDATE(OBJECT.NAME, "", OBJECT.VALUE)
   END

   IF OUTPUT.DATA <> "" THEN
      ID.SIENA.STAGING = "RV":"ONIA":U_SEP_ALT:TRIM(CCY)
      CALL U_CRT.INFO("DELETE", ID.SIENA.STAGING)
      CALL U_IO.DELETE(FV.SIENA.STAGING, ID.SIENA.STAGING, STOP.ON.ERROR, ERROR.TEXT)
      CALL U_CRT.INFO("STORE", ID.SIENA.STAGING)
      CALL U_IO.WRITE(FV.SIENA.STAGING, ID.SIENA.STAGING, OUTPUT.DATA, STOP.ON.ERROR, ERROR.TEXT)


   END

   RETURN

END
