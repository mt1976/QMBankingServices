SUBROUTINE U_MSSQL.SELECT(SQL.FIELDS, SQL.TABLE, SQL.DATABASE, SQL.WHERE, VERBOSE, ID.RESULT, RESULT.COLUMNS,RESULT.DATA,RESULT.COUNT)
** INFORMATION ****************************************************************
* Routine Name   : U_MSSQL.SELECT
*         Type   : SUBROUTINE
*         Loc    : UTIL.BP
* -----------------------------------------------------------------------------
* Info Updated   : 20191218 at 15.43.50 in MWT-QM-DEV by sales
*                : on ldn-srv-ubnt01 (Linux)
* -----------------------------------------------------------------------------
*
*******************************************************************************
$INCLUDE UTIL.BP I_UTIL.H
$INCLUDE UTIL.BP I_UTIL.LOG.EVENT.H

RESULT.DATA = ""

FN.UTIL.SQL.RESPONSE = "UTIL.SQL.RESPONSE"
FV.UTIL.SQL.RESPONSE = ""
FP.UTIL.SQL.RESPONSE = ""
STOP.ON.ERROR        = @TRUE
ERROR.TEXT           = ""

CALL U_OPENFILE(FN.UTIL.SQL.RESPONSE, FV.UTIL.SQL.RESPONSE, STOP.ON.ERROR, ERROR.TEXT)
CALL U_OS.FILE.PATH(FV.UTIL.SQL.RESPONSE, FP.UTIL.SQL.RESPONSE)
CALL U_CRT.INFO(FN.UTIL.SQL.RESPONSE,FP.UTIL.SQL.RESPONSE)

CALL U_OS.GET.PROPERTY("UTIL.CONFIG", "mssql", "resultDelim", SQL.DELIM)

IN.STATEMENT = ""
IN.STATEMENT = IN.STATEMENT:"select "
IF SQL.FIELDS <> "" THEN
  IN.STATEMENT = IN.STATEMENT:SQL.FIELDS
END ELSE
  IN.STATEMENT = IN.STATEMENT:"*"
END
IN.STATEMENT = IN.STATEMENT:" from ":SQL.TABLE
IF SQL.WHERE <> "" THEN
  IN.STATEMENT = IN.STATEMENT:" where ":SQL.WHERE
END

CALL U_MSSQL.EXECUTE(IN.STATEMENT,SQL.DATABASE,@TRUE,ID.RESULT)

CALL U_READ(FV.UTIL.SQL.RESPONSE, ID.RESULT, R.UTIL.SQL.RESPONSE, STOP.ON.ERROR, ERROR.TEXT)

NO.COLUMNS = DCOUNT(R.UTIL.SQL.RESPONSE<2>,SQL.DELIM)-2

CALL U_CRT.INFO("NO.FIELDS",DCOUNT(SQL.FIELDS,","))
CALL U_CRT.INFO("NO.COLUMNS",NO.COLUMNS)

* get column names
RESULT.COLUMNS = TRIM(CHANGE(R.UTIL.SQL.RESPONSE<2>,SQL.DELIM,@AM))
DEL RESULT.COLUMNS<1>
NO.CDS = DCOUNT(RESULT.COLUMNS,@AM)
DEL RESULT.COLUMNS<NO.CDS>
RESULT.COLUMNS = TRIMWS(RESULT.COLUMNS)
CALL U_CRT.RECORD(RESULT.COLUMNS)

NO.ROWS = DCOUNT(R.UTIL.SQL.RESPONSE,@AM)
START.ROW = 4
END.ROW   = NO.ROWS - 2
FOR PROCESS.ROW = START.ROW TO END.ROW
RESULT.DATA.TMP = CHANGE(R.UTIL.SQL.RESPONSE<PROCESS.ROW>,SQL.DELIM,@VM)
DEL RESULT.DATA.TMP<1,1>
DEL RESULT.DATA.TMP<1,NO.CDS>
RESULT.DATA<-1> = TRIMWS(RESULT.DATA.TMP)
NEXT PROCESS.ROW
RESULT.COUNT = DCOUNT(RESULT.DATA,@AM)
   RETURN
END
