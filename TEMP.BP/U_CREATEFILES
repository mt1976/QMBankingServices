PROGRAM U_CREATEFILES
$INCLUDE UTIL.BP UTIL.H
   PNM = "" ; STOP.ON.ERROR = @TRUE ; ERROR.TEXT = ""
   CALL U_START(PNM)
   CALL U_HEADER("CREATE FILE(S)")

   FN.UTIL.CONFIG = "UTIL.CONFIG" ; FV.UTIL.CONFIG = ""
   CALL U_OPENFILE(FN.UTIL.CONFIG, FV.UTIL.CONFIG, STOP.ON.ERROR, ERROR.TEXT)

   CMD.STRING = CONVERT(SPACE(1), @AM, @SENTENCE)
   IF CMD.STRING<2> = "" THEN
      CALL U_CRT.ACTION("ERROR", "Parameters [ALL, or specific i.e. UTILS/SIENA]")
      CALL U_STOP(PNM)
      STOP
   END

   * remove the program name from the stack of cmds
   DEL CMD.STRING<1>
   * loop through the cmd strings
   NO.CMDS = DCOUNT(CMD.STRING, @AM)

   CALL U_CRT.ACTION("NO PROCESSES", NO.CMDS)
   FOR THIS.CMD = 1 TO NO.CMDS
      DELIVERY.TYPE = CMD.STRING<THIS.CMD>
      CALL U_CRT.ACTION("PROCESS", DELIVERY.TYPE)
      STMT = "SELECT ":FN.UTIL.CONFIG:" LIKE create.":DOWNCASE(DELIVERY.TYPE):".properties"
      IF DELIVERY.TYPE = "ALL" THEN
         STMT = "SELECT ":FN.UTIL.CONFIG:" LIKE create":"...":"properties"
      END
      GOSUB PROCESS.IT
   NEXT THIS.CMD

   CALL U_STOP(PNM)

   STOP
PROCESS.IT:
   CALL U_CRT.ACTION('STMT', STMT)

   CALL U_EXECUTE(STMT, @TRUE)

   PROCESS.LIST = ""
   LOOP
      READNEXT ID.FILE ELSE EXIT
      PROCESS.LIST<-1> = ID.FILE
   REPEAT

   NO.PROPERTIES.FILES = DCOUNT(PROCESS.LIST, @AM)
   CALL U_CRT.ACTION("NO PROPERTIES", NO.PROPERTIES.FILES)
   CALL U_CRT.RECORD(PROCESS.LIST)

   FOR THIS.PROPERTIES.FILE = 1 TO NO.PROPERTIES.FILES
      CALL U_CRT.ACTION("PROPERTIES", PROCESS.LIST<THIS.PROPERTIES.FILE>)
      CALL U_READ(FV.UTIL.CONFIG, PROCESS.LIST<THIS.PROPERTIES.FILE>, R.UTIL.CONFIG, STOP.ON.ERROR, ERROR.TEXT)
      NO.FILES.TO.CLEAR = DCOUNT(R.UTIL.CONFIG, @AM)
      CALL U_CRT.ACTION("FILES", NO.FILES.TO.CLEAR)
      CALL U_CRT.RECORD(R.UTIL.CONFIG)

      FOR THIS.FILE = 1 TO NO.FILES.TO.CLEAR

         PROCESS.ITEM = CONVERT(U_PROP_SEP, @AM, R.UTIL.CONFIG<THIS.FILE>)
         PROCESS.WHAT = PROCESS.ITEM<1>
         PROCESS.FILE = PROCESS.ITEM<2>
         IF PROCESS.WHAT = "FILE" THEN
            EX.STMT = "CREATE-FILE ":PROCESS.FILE:" DIRECTORY"
            CALL U_CRT.ACTION("CREATE", PROCESS.FILE)
            CALL U_EXECUTE(EX.STMT, @TRUE)
         END
      NEXT THIS.FILE

   NEXT THIS.PROPERTIES.FILE
   RETURN


END
